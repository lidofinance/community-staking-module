// SPDX-FileCopyrightText: 2026 Lido <info@lido.fi>
// SPDX-License-Identifier: GPL-3.0

pragma solidity 0.8.33;

import { BeaconBlockHeader, Slot, Validator, Withdrawal } from "../lib/Types.sol";
import { GIndex } from "../lib/GIndex.sol";

import { IBaseModule } from "./IBaseModule.sol";

interface IVerifier {
    struct RecentHeaderWitness {
        BeaconBlockHeader header; // Header of a block which root is a root at rootsTimestamp.
        uint64 rootsTimestamp; // To be passed to the EIP-4788 block roots contract.
    }

    // A witness for a block header which root is accessible either via historical_summaries or block_roots.
    struct HistoricalHeaderWitness {
        BeaconBlockHeader header;
        bytes32[] proof;
    }

    struct WithdrawalWitness {
        uint8 offset; // In the withdrawals list.
        Withdrawal object;
        bytes32[] proof;
    }

    struct ValidatorWitness {
        uint64 index; // Index of a validator in a Beacon state.
        uint32 nodeOperatorId;
        uint32 keyIndex; // Index of the withdrawn key in the Node Operator's keys storage.
        Validator object;
        bytes32[] proof;
    }

    struct BalanceWitness {
        bytes32 node;
        bytes32[] proof;
    }

    struct ProcessSlashedInput {
        ValidatorWitness validator;
        RecentHeaderWitness recentBlock;
    }

    /// @notice Withdrawal proof input shared by the recent and historical withdrawal flows.
    /// @dev `withdrawalBlock.proof` proves the withdrawal block against `recentBlock` state. The recent flow resolves
    /// it through `block_roots`, while the historical flow resolves it through `historical_summaries`.
    struct ProcessWithdrawalInput {
        WithdrawalWitness withdrawal;
        ValidatorWitness validator;
        RecentHeaderWitness recentBlock;
        // The block that actually contained the withdrawal.
        HistoricalHeaderWitness withdrawalBlock;
    }

    struct ProcessBalanceProofInput {
        RecentHeaderWitness recentBlock;
        // The block containing the balance, proven against `recentBlock` state block roots.
        HistoricalHeaderWitness balanceBlock;
        ValidatorWitness validator;
        BalanceWitness balance;
    }

    struct ProcessHistoricalBalanceProofInput {
        RecentHeaderWitness recentBlock;
        HistoricalHeaderWitness historicalBlock;
        ValidatorWitness validator;
        BalanceWitness balance;
    }

    error RootNotFound();
    error InvalidBlockHeader();
    error InvalidChainConfig();
    error PartialWithdrawal();
    error ValidatorIsSlashed();
    error ValidatorIsNotSlashed();
    error ValidatorIsNotWithdrawable();
    error ValidatorIsWithdrawable();
    error InvalidWithdrawalCredentials();
    error InvalidWithdrawalAddress();
    error InvalidPublicKey();
    error InvalidValidatorIndex();
    error UnsupportedSlot(Slot slot);
    error ZeroModuleAddress();
    error ZeroWithdrawalCredentials();
    error ZeroAdminAddress();
    error InvalidGloasSlot();
    error InvalidCapellaSlot();
    error InvalidMinWithdrawalRatio();
    error HistoricalSummaryDoesNotExist();
    error BlockRootNotInRange();

    function BEACON_ROOTS() external view returns (address);

    function SLOTS_PER_EPOCH() external view returns (uint64);

    function SLOTS_PER_HISTORICAL_ROOT() external view returns (uint64);

    function GI_WITHDRAWALS_PRE_GLOAS() external view returns (GIndex);

    function GI_WITHDRAWALS() external view returns (GIndex);

    function GI_VALIDATORS_PRE_GLOAS() external view returns (GIndex);

    function GI_VALIDATORS() external view returns (GIndex);

    function GI_HISTORICAL_SUMMARIES_PRE_GLOAS() external view returns (GIndex);

    function GI_HISTORICAL_SUMMARIES() external view returns (GIndex);

    function GI_BLOCK_ROOT_IN_SUMMARY() external view returns (GIndex);

    function GI_BLOCK_ROOTS_PRE_GLOAS() external view returns (GIndex);

    function GI_BLOCK_ROOTS() external view returns (GIndex);

    function FIRST_SUPPORTED_SLOT() external view returns (Slot);

    function GLOAS_SLOT() external view returns (Slot);

    function CAPELLA_SLOT() external view returns (Slot);

    function WITHDRAWAL_CREDENTIALS() external view returns (bytes32);

    function MIN_WITHDRAWAL_RATIO() external view returns (uint256);

    function MODULE() external view returns (IBaseModule);

    /// @notice Verify proof of a slashed validator and report it to the module
    /// @param data @see ProcessSlashedInput
    function processSlashedProof(ProcessSlashedInput calldata data) external;

    /// @notice Verify a withdrawal block through recent state `block_roots` and report the withdrawal to the module.
    /// @notice The method doesn't accept proofs for slashed validators. A dedicated committee is responsible for
    /// determining the exact penalty amounts and calling the `IBaseModule.reportSlashedWithdrawnValidators` method via
    /// an EasyTrack motion.
    /// @param data @see ProcessWithdrawalInput
    function processWithdrawalProof(ProcessWithdrawalInput calldata data) external;

    /// @notice Verify a withdrawal block through recent state `historical_summaries` and report the withdrawal to the module.
    /// @notice The method doesn't accept proofs for slashed validators. A dedicated committee is responsible for
    /// determining the exact penalty amounts and calling the `IBaseModule.reportSlashedWithdrawnValidators` method via
    /// an EasyTrack motion.
    /// @param data @see ProcessWithdrawalInput
    function processHistoricalWithdrawalProof(ProcessWithdrawalInput calldata data) external;

    /// @notice Verify a validator's balance proof from a beacon block available through recent state block roots.
    /// @param data The balance proof input containing recent and balance block headers and proof witnesses.
    function processBalanceProof(ProcessBalanceProofInput calldata data) external;

    /// @notice Verify a validator's balance proof from a historical beacon block and sync the key added balance.
    ///         A historical proof is needed because the validator's balance may have increased at some point in the past
    ///         and later decreased (e.g. due to inactivity leak or penalties). A recent proof alone would miss that peak,
    ///         so a historical proof allows capturing the highest observed balance.
    /// @param data The balance proof input containing recent + historical block headers, validator witness, and balance witness.
    function processHistoricalBalanceProof(ProcessHistoricalBalanceProofInput calldata data) external;
}
