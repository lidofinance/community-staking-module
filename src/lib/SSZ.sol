// SPDX-FileCopyrightText: 2026 Lido <info@lido.fi>
// SPDX-License-Identifier: GPL-3.0

pragma solidity 0.8.33;

import { BeaconBlockHeader, Withdrawal, Validator } from "./Types.sol";
import { GIndex } from "./GIndex.sol";

library SSZ {
    error BranchHasMissingItem();
    error BranchHasExtraItem();
    error InvalidProof();

    function hashTreeRoot(BeaconBlockHeader memory header) internal view returns (bytes32 root) {
        bytes32[8] memory nodes = [
            toLittleEndian(header.slot.unwrap()),
            toLittleEndian(header.proposerIndex),
            header.parentRoot,
            header.stateRoot,
            header.bodyRoot,
            bytes32(0),
            bytes32(0),
            bytes32(0)
        ];

        root = _merkleize8(nodes);
    }

    function hashTreeRoot(Validator memory validator) internal view returns (bytes32 root) {
        bytes32 pubkeyRoot;
        assembly ("memory-safe") {
            // Dynamic data types such as bytes are stored at the specified offset.
            let offset := mload(validator)
            // Copy the pubkey to the scratch space.
            mcopy(0x00, add(offset, 32), 48)
            // Clear the last 16 bytes.
            mcopy(48, 0x60, 16)
            // Call sha256 precompile. Return code unchecked: sha256 can only fail with OOG,
            // and all gas is forwarded, so OOG here means OOG for the caller.
            pop(staticcall(gas(), 0x02, 0x00, 0x40, 0x00, 0x20))

            pubkeyRoot := mload(0x00)
        }

        bytes32[8] memory nodes = [
            pubkeyRoot,
            validator.withdrawalCredentials,
            toLittleEndian(validator.effectiveBalance),
            toLittleEndian(validator.slashed),
            toLittleEndian(validator.activationEligibilityEpoch),
            toLittleEndian(validator.activationEpoch),
            toLittleEndian(validator.exitEpoch),
            toLittleEndian(validator.withdrawableEpoch)
        ];

        root = _merkleize8(nodes);
    }

    function _merkleize8(bytes32[8] memory nodes) private view returns (bytes32 root) {
        assembly ("memory-safe") {
            // Return codes are unchecked: sha256 can only fail with OOG, and all gas is forwarded.
            // Hash the eight leaves into four nodes, overwriting leaves that are no longer needed.
            pop(staticcall(gas(), 0x02, nodes, 0x40, nodes, 0x20))
            pop(staticcall(gas(), 0x02, add(nodes, 0x40), 0x40, add(nodes, 0x20), 0x20))
            pop(staticcall(gas(), 0x02, add(nodes, 0x80), 0x40, add(nodes, 0x40), 0x20))
            pop(staticcall(gas(), 0x02, add(nodes, 0xc0), 0x40, add(nodes, 0x60), 0x20))

            // Hash the four intermediate nodes into two nodes.
            pop(staticcall(gas(), 0x02, nodes, 0x40, nodes, 0x20))
            pop(staticcall(gas(), 0x02, add(nodes, 0x40), 0x40, add(nodes, 0x20), 0x20))

            // Hash the final pair and return the root.
            pop(staticcall(gas(), 0x02, nodes, 0x40, nodes, 0x20))
            root := mload(nodes)
        }
    }

    /// @notice Modified version of `verify` from Solady `MerkleProofLib` to support generalized indices and sha256 precompile.
    /// @dev Reverts if `leaf` doesn't exist in the Merkle tree with `root`, given `proof`.
    function verifyProof(bytes32[] calldata proof, bytes32 root, bytes32 leaf, GIndex gI) internal view {
        uint256 index = gI.unwrap();

        assembly ("memory-safe") {
            // Check if `proof` is empty.
            if iszero(proof.length) {
                // revert InvalidProof()
                mstore(0x00, 0x09bde339)
                revert(0x1c, 0x04)
            }
            // Left shift by 5 is equivalent to multiplying by 0x20.
            let end := add(proof.offset, shl(5, proof.length))
            // Initialize `offset` to the offset of `proof` in the calldata.
            let offset := proof.offset
            // Iterate over proof elements to compute root hash.
            // prettier-ignore
            for {} 1 {} {
                // Slot of `leaf` in scratch space.
                // If the condition is true: 0x20, otherwise: 0x00.
                let scratch := shl(5, and(index, 1))
                index := shr(1, index)
                if iszero(index) {
                    // revert BranchHasExtraItem()
                    mstore(0x00, 0x5849603f)
                    // 0x1c = 28 => offset in 32-byte word of a slot 0x00
                    revert(0x1c, 0x04)
                }
                // Store elements to hash contiguously in scratch space.
                // Scratch space is 64 bytes (0x00 - 0x3f) and both elements are 32 bytes.
                mstore(scratch, leaf)
                mstore(xor(scratch, 0x20), calldataload(offset))
                // Call sha256 precompile. Return code unchecked: sha256 can only fail with OOG,
                // and all gas is forwarded, so OOG here means OOG for the caller.
                pop(staticcall(gas(), 0x02, 0x00, 0x40, 0x00, 0x20))

                // Reuse `leaf` to store the hash to reduce stack operations.
                leaf := mload(0x00)
                offset := add(offset, 0x20)
                if iszero(lt(offset, end)) {
                    break
                }
            }

            if iszero(eq(index, 1)) {
                // revert BranchHasMissingItem()
                mstore(0x00, 0x1b6661c3)
                revert(0x1c, 0x04)
            }

            if iszero(eq(leaf, root)) {
                // revert InvalidProof()
                mstore(0x00, 0x09bde339)
                revert(0x1c, 0x04)
            }
        }
    }

    // Inspired by https://github.com/succinctlabs/telepathy-contracts/blob/5aa4bb7/src/libraries/SimpleSerialize.sol#L59
    function hashTreeRoot(Withdrawal memory withdrawal) internal pure returns (bytes32) {
        return
            sha256(
                bytes.concat(
                    sha256(bytes.concat(toLittleEndian(withdrawal.index), toLittleEndian(withdrawal.validatorIndex))),
                    sha256(
                        bytes.concat(
                            bytes20(withdrawal.withdrawalAddress),
                            bytes12(0),
                            toLittleEndian(withdrawal.amount)
                        )
                    )
                )
            );
    }

    function toLittleEndian(uint256 v) internal pure returns (bytes32) {
        return endianReverse(bytes32(v));
    }

    function toLittleEndian(bool v) internal pure returns (bytes32) {
        return bytes32(v ? 1 << 248 : 0);
    }

    // See https://github.com/succinctlabs/telepathy-contracts/blob/5aa4bb7/src/libraries/SimpleSerialize.sol#L17-L28
    function endianReverse(bytes32 v) internal pure returns (bytes32) {
        v =
            ((v & 0xFF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00) >> 8) |
            ((v & 0x00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF) << 8);
        v =
            ((v & 0xFFFF0000FFFF0000FFFF0000FFFF0000FFFF0000FFFF0000FFFF0000FFFF0000) >> 16) |
            ((v & 0x0000FFFF0000FFFF0000FFFF0000FFFF0000FFFF0000FFFF0000FFFF0000FFFF) << 16);
        v =
            ((v & 0xFFFFFFFF00000000FFFFFFFF00000000FFFFFFFF00000000FFFFFFFF00000000) >> 32) |
            ((v & 0x00000000FFFFFFFF00000000FFFFFFFF00000000FFFFFFFF00000000FFFFFFFF) << 32);
        v =
            ((v & 0xFFFFFFFFFFFFFFFF0000000000000000FFFFFFFFFFFFFFFF0000000000000000) >> 64) |
            ((v & 0x0000000000000000FFFFFFFFFFFFFFFF0000000000000000FFFFFFFFFFFFFFFF) << 64);
        v = (v >> 128) | (v << 128);
        return v;
    }
}
