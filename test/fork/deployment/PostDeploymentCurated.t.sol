// SPDX-FileCopyrightText: 2026 Lido <info@lido.fi>
// SPDX-License-Identifier: GPL-3.0

pragma solidity 0.8.33;

import { Test } from "forge-std/Test.sol";

import { Initializable } from "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";

import { CuratedDeployParams, CuratedGateConfig, GateCurveParams } from "script/curated/DeployBase.s.sol";
import { CuratedGate } from "src/CuratedGate.sol";
import { ICuratedModule } from "src/interfaces/ICuratedModule.sol";
import { IMetaRegistry } from "src/interfaces/IMetaRegistry.sol";
import { IParametersRegistry } from "src/interfaces/IParametersRegistry.sol";
import { Step } from "src/interfaces/IStepwiseWeightBoost.sol";

import { Utilities } from "../../helpers/Utilities.sol";
import { DeploymentFixtures } from "../../helpers/Fixtures.sol";

/// @dev Minimal view of the wiring every weight boost provider exposes, so assertions can be shared across
///      providers with different concrete types.
interface IStepwiseProviderWiring {
    function MODULE() external view returns (address);

    function META_REGISTRY() external view returns (address);
}

contract DeploymentBaseTest is Test, Utilities, DeploymentFixtures {
    CuratedDeployParams internal deployParams;
    CuratedGateConfig[] internal deployGateConfigs;
    uint256 internal adminsCount;

    /// @dev Asserts neither the proxy nor its implementation can be initialized again.
    function _assertNotReinitializable(address proxyAddress, address impl, bytes memory initCalldata) internal {
        _assertInvalidInitialization(proxyAddress, initCalldata);
        _assertInvalidInitialization(impl, initCalldata);
    }

    /// @dev Asserts the provider points at the deployed module and MetaRegistry.
    function _assertProviderWiring(address provider, string memory label) internal view {
        assertEq(
            IStepwiseProviderWiring(provider).MODULE(),
            address(curatedModule),
            string.concat(label, " module wiring")
        );
        assertEq(
            IStepwiseProviderWiring(provider).META_REGISTRY(),
            address(metaRegistry),
            string.concat(label, " meta registry wiring")
        );
    }

    /// @dev Asserts the configured step function round-trips through the provider.
    function _assertSteps(Step[] memory actual, Step[] memory expected) internal view {
        assertEq(actual.length, expected.length, "unexpected steps count");
        for (uint256 i; i < expected.length; ++i) {
            assertEq(actual[i].threshold, expected[i].threshold, "unexpected step threshold");
            assertEq(actual[i].value, expected[i].value, "unexpected step value");
        }
    }

    function _assertInvalidInitialization(address target, bytes memory initCalldata) private {
        (bool success, bytes memory returnData) = target.call(initCalldata);
        assertFalse(success, "initialize did not revert");
        assertEq(bytes4(returnData), Initializable.InvalidInitialization.selector, "unexpected initialize revert");
    }

    function setUp() public {
        Env memory env = envVars();
        vm.createSelectFork(env.RPC_URL);
        initializeFromDeployment();
        if (moduleType != ModuleType.Curated) vm.skip(true, "Current deployment is not Curated module type");
        adminsCount = block.chainid == 1 ? 1 : 2;
        string memory config = vm.readFile(env.DEPLOY_CONFIG);
        // mutates storage variable
        updateCuratedDeployParams(deployParams, env.DEPLOY_CONFIG);
    }
}

contract ModuleDeploymentTest is DeploymentBaseTest {
    function test_state_onlyFull() public view {
        assertEq(module.getInitializedVersion(), 1);
    }

    function test_roles_onlyFull() public view {
        bytes32 role = module.CREATE_NODE_OPERATOR_ROLE();
        uint256 gatesCount = curatedGates.length;
        assertEq(module.getRoleMemberCount(role), gatesCount);

        for (uint256 i = 0; i < gatesCount; ++i) {
            assertTrue(module.hasRole(role, curatedGates[i]), "gate missing module role");
        }
        assertEq(
            module.getRoleMemberCount(curatedModule.OPERATOR_ADDRESSES_ADMIN_ROLE()),
            0,
            "unexpected operator addresses admin role members"
        );
    }

    function test_initialization_onlyFull() public {
        vm.expectRevert(Initializable.InvalidInitialization.selector);
        curatedModule.initialize({ admin: deployParams.aragonAgent });

        vm.expectRevert(Initializable.InvalidInitialization.selector);
        ICuratedModule(address(moduleImpl)).initialize({ admin: deployParams.aragonAgent });
    }

    function test_proxy_onlyFull() public view {
        _assertProxy(address(curatedModule), address(moduleImpl), deployParams.proxyAdmin, "curated module");
    }
}

contract MetaRegistryDeploymentTest is DeploymentBaseTest {
    function _assertWeightBoostProvider(
        uint256 expectedProviderId,
        address expectedProvider,
        IMetaRegistry.WeightBoostProviderMode expectedMode
    ) internal view {
        uint256 providerId = metaRegistry.getWeightBoostProviderId(expectedProvider);
        assertEq(providerId, expectedProviderId, "unexpected weight boost provider ID");

        IMetaRegistry.WeightBoostProviderEntry memory entry = metaRegistry.getWeightBoostProvider(expectedProviderId);
        assertEq(address(entry.provider), expectedProvider, "unexpected weight boost provider");
        assertEq(uint256(entry.mode), uint256(expectedMode), "unexpected weight boost provider mode");
        assertTrue(entry.enabled, "weight boost provider disabled");
    }

    function test_state_onlyFull() public view {
        assertEq(metaRegistry.getInitializedVersion(), 1);
        assertEq(metaRegistry.getOperatorGroupsCount(), 0);

        IMetaRegistry.OperatorGroup memory groupInfo = metaRegistry.getOperatorGroup(metaRegistry.NO_GROUP_ID());
        assertEq(groupInfo.subNodeOperators.length, 0);
        assertEq(groupInfo.externalOperators.length, 0);
    }

    function test_roles_onlyFull() public view {
        _checkAdminRole(address(metaRegistry), deployParams.aragonAgent, deployParams.secondAdminAddress);

        bytes32 setterRole = metaRegistry.SET_OPERATOR_INFO_ROLE();
        uint256 gatesCount = curatedGates.length;
        assertEq(metaRegistry.getRoleMemberCount(setterRole), gatesCount + 1, "unexpected setter role members count"); // +1 for setOperatorInfoManager
        for (uint256 i = 0; i < gatesCount; ++i) {
            assertTrue(metaRegistry.hasRole(setterRole, curatedGates[i]), "gate missing metaRegistry setter role");
        }
        assertTrue(
            metaRegistry.hasRole(setterRole, deployParams.setOperatorInfoManager),
            "missing setOperatorInfoManager role"
        );

        assertTrue(
            metaRegistry.hasRole(metaRegistry.MANAGE_OPERATOR_GROUPS_ROLE(), deployParams.easyTrackEVMScriptExecutor),
            "missing easyTrackEVMScriptExecutor manage operator groups role"
        );

        assertEq(
            metaRegistry.getRoleMemberCount(metaRegistry.MANAGE_OPERATOR_GROUPS_ROLE()),
            1,
            "unexpected manage operator groups role members count"
        );

        assertEq(
            metaRegistry.getRoleMemberCount(metaRegistry.SET_BOND_CURVE_WEIGHT_ROLE()),
            0,
            "unexpected set bond curve weight role members count"
        );
    }

    function test_weightBoostProviders_onlyFull() public view {
        assertEq(metaRegistry.getWeightBoostProvidersCount(), 4, "unexpected weight boost providers count");
        _assertWeightBoostProvider(
            1,
            address(additionalBondRegistry),
            IMetaRegistry.WeightBoostProviderMode.PerNodeOperator
        );
        _assertWeightBoostProvider(
            2,
            address(nodeOperatorStrikes),
            IMetaRegistry.WeightBoostProviderMode.PerNodeOperator
        );
        _assertWeightBoostProvider(3, address(ldoLockBoostProvider), IMetaRegistry.WeightBoostProviderMode.MaxPerGroup);
        _assertWeightBoostProvider(
            4,
            address(customFeeRegistry),
            IMetaRegistry.WeightBoostProviderMode.PerNodeOperator
        );
    }
}

contract AdditionalBondRegistryDeploymentTest is DeploymentBaseTest {
    function test_state_onlyFull() public view {
        assertEq(additionalBondRegistry.getInitializedVersion(), 1);
        // Curve multiplier thresholds map to weight multiplier increments.
        _assertSteps(additionalBondRegistry.getSteps(), deployParams.additionalBondRegistryConfig.boostSteps);
        assertEq(
            additionalBondRegistry.getCurveMultiplierReductionCooldown(),
            deployParams.additionalBondRegistryConfig.curveMultiplierReductionCooldown,
            "additional bond registry cooldown"
        );
    }

    function test_immutables_onlyFull() public view {
        _assertProviderWiring(address(additionalBondRegistry), "additional bond registry");
        assertEq(
            address(additionalBondRegistry.ACCOUNTING()),
            address(accounting),
            "additional bond registry accounting"
        );
        assertEq(
            additionalBondRegistry.MAX_CURVE_MULTIPLIER(),
            90_000,
            "additional bond registry max curve multiplier"
        );
        assertEq(additionalBondRegistry.CURVE_MULTIPLIER_STEP(), 100, "additional bond registry curve multiplier step");
        assertEq(
            additionalBondRegistry.MAX_CURVE_MULTIPLIER_REDUCTION_COOLDOWN(),
            365 days,
            "additional bond registry max cooldown"
        );
        assertEq(additionalBondRegistry.MAX_STEPS(), 35, "additional bond registry max steps");
        assertEq(additionalBondRegistry.MAX_STEP_VALUE(), 90_000, "additional bond registry max weight multiplier");
    }

    function test_roles_onlyFull() public view {
        _checkAdminRole(address(additionalBondRegistry), deployParams.aragonAgent, deployParams.secondAdminAddress);

        // AdditionalBondRegistry must be able to update the operator curve multiplier in Accounting.
        assertTrue(
            accounting.hasRole(accounting.SET_BOND_CURVE_MULTIPLIER_ROLE(), address(additionalBondRegistry)),
            "additional bond registry missing accounting set curve multiplier role"
        );
    }

    function test_initialization_onlyFull() public {
        _assertNotReinitializable(
            address(additionalBondRegistry),
            address(additionalBondRegistryImpl),
            abi.encodeCall(additionalBondRegistry.initialize, (deployParams.aragonAgent, 7 days, new Step[](0)))
        );
    }

    function test_proxy_onlyFull() public view {
        _assertProxy(
            address(additionalBondRegistry),
            address(additionalBondRegistryImpl),
            deployParams.proxyAdmin,
            "additional bond registry"
        );
    }
}

contract NodeOperatorStrikesDeploymentTest is DeploymentBaseTest {
    function test_state_onlyFull() public view {
        assertEq(nodeOperatorStrikes.getInitializedVersion(), 1);
        // Strike count thresholds map to weight reductions in basis points.
        _assertSteps(nodeOperatorStrikes.getSteps(), deployParams.nodeOperatorStrikesConfig.thresholds);
    }

    function test_immutables_onlyFull() public view {
        _assertProviderWiring(address(nodeOperatorStrikes), "node operator strikes");
        assertEq(nodeOperatorStrikes.MAX_DESCRIPTION_LENGTH(), 1024, "strikes max description length");
        assertEq(nodeOperatorStrikes.MAX_STEPS(), 35, "strikes max steps");
        assertEq(nodeOperatorStrikes.MAX_STEP_VALUE(), 90_000, "strikes max step value");
    }

    function test_roles_onlyFull() public view {
        _checkAdminRole(address(nodeOperatorStrikes), deployParams.aragonAgent, deployParams.secondAdminAddress);

        bytes32 committeeRole = nodeOperatorStrikes.STRIKES_COMMITTEE_ROLE();
        assertEq(nodeOperatorStrikes.getRoleMemberCount(committeeRole), 1);
        assertTrue(nodeOperatorStrikes.hasRole(committeeRole, deployParams.nodeOperatorStrikesConfig.committee));
    }

    function test_initialization_onlyFull() public {
        _assertNotReinitializable(
            address(nodeOperatorStrikes),
            address(nodeOperatorStrikesImpl),
            abi.encodeCall(nodeOperatorStrikes.initialize, (deployParams.aragonAgent, new Step[](0)))
        );
    }

    function test_proxy_onlyFull() public view {
        _assertProxy(
            address(nodeOperatorStrikes),
            address(nodeOperatorStrikesImpl),
            deployParams.proxyAdmin,
            "strikes"
        );
    }
}

contract LDOLockBoostProviderDeploymentTest is DeploymentBaseTest {
    function test_state_onlyFull() public view {
        assertEq(ldoLockBoostProvider.getInitializedVersion(), 1);
        assertEq(
            ldoLockBoostProvider.getLockPeriod(),
            deployParams.ldoLockBoostProviderConfig.lockPeriod,
            "LDO lock provider lock period"
        );

        // Locked amount thresholds map to weight boosts in basis points.
        _assertSteps(ldoLockBoostProvider.getSteps(), deployParams.ldoLockBoostProviderConfig.lockBoostSteps);
    }

    function test_immutables_onlyFull() public view {
        _assertProviderWiring(address(ldoLockBoostProvider), "LDO lock provider");
        assertEq(
            ldoLockBoostProvider.TOKEN(),
            deployParams.ldoLockBoostProviderConfig.token,
            "LDO lock provider token"
        );
        assertEq(
            address(ldoLockBoostProvider.VAULT_BEACON()),
            address(ldoLockVaultBeacon),
            "LDO lock provider vault beacon"
        );
        assertEq(
            ldoLockBoostProvider.MIN_LOCK_PERIOD(),
            deployParams.ldoLockBoostProviderConfig.minLockPeriod,
            "LDO lock provider min lock period"
        );
        assertEq(ldoLockBoostProvider.MAX_LOCK_PERIOD(), 365 days, "LDO lock provider max lock period");
        assertEq(ldoLockBoostProvider.MAX_STEPS(), 35, "LDO lock provider max steps");
        assertEq(ldoLockBoostProvider.MAX_STEP_VALUE(), 90_000, "LDO lock provider max step value");
    }

    function test_roles_onlyFull() public view {
        _checkAdminRole(address(ldoLockBoostProvider), deployParams.aragonAgent, deployParams.secondAdminAddress);
    }

    function test_vaultBeacon_onlyFull() public view {
        assertGt(address(ldoLockVaultImpl).code.length, 0, "LDO lock vault impl code");
        assertGt(address(ldoLockVaultBeacon).code.length, 0, "LDO lock vault beacon code");
        assertEq(ldoLockVaultBeacon.implementation(), address(ldoLockVaultImpl), "LDO lock vault beacon impl");
        assertEq(ldoLockVaultBeacon.owner(), deployParams.aragonAgent, "LDO lock vault beacon owner");
    }

    function test_vaultImplementation_onlyFull() public view {
        assertEq(ldoLockVaultImpl.nodeOperatorId(), 0, "LDO lock vault impl node operator ID");
        assertEq(ldoLockVaultImpl.TOKEN(), deployParams.ldoLockBoostProviderConfig.token, "LDO lock vault impl token");
        assertEq(ldoLockVaultImpl.PROVIDER(), address(ldoLockBoostProvider), "LDO lock vault impl provider");
        assertEq(address(ldoLockVaultImpl.MODULE()), address(curatedModule), "LDO lock vault impl module");
        assertEq(
            ldoLockVaultImpl.VOTING_CONTRACT(),
            deployParams.ldoLockBoostProviderConfig.votingContract,
            "LDO lock vault impl voting"
        );
        assertEq(
            ldoLockVaultImpl.snapshotDelegation(),
            deployParams.ldoLockBoostProviderConfig.snapshotDelegation,
            "LDO lock vault impl snapshot delegation"
        );
    }

    function test_initialization_onlyFull() public {
        _assertNotReinitializable(
            address(ldoLockBoostProvider),
            address(ldoLockBoostProviderImpl),
            abi.encodeCall(
                ldoLockBoostProvider.initialize,
                (
                    deployParams.aragonAgent,
                    deployParams.ldoLockBoostProviderConfig.lockPeriod,
                    deployParams.ldoLockBoostProviderConfig.lockBoostSteps
                )
            )
        );

        vm.expectRevert(Initializable.InvalidInitialization.selector);
        ldoLockVaultImpl.initialize(0);
    }

    function test_proxy_onlyFull() public view {
        _assertProxy(
            address(ldoLockBoostProvider),
            address(ldoLockBoostProviderImpl),
            deployParams.proxyAdmin,
            "LDO lock provider"
        );
    }
}

contract CustomFeeRegistryDeploymentTest is DeploymentBaseTest {
    function test_state_onlyFull() public view {
        assertEq(customFeeRegistry.getInitializedVersion(), 1);
        assertEq(
            customFeeRegistry.getFeeShareDiscountCutCooldown(),
            deployParams.customFeeRegistryConfig.feeShareDiscountCutCooldown
        );

        _assertSteps(customFeeRegistry.getSteps(), deployParams.customFeeRegistryConfig.boostSteps);
    }

    function test_immutables_onlyFull() public view {
        _assertProviderWiring(address(customFeeRegistry), "custom fee registry");
        assertEq(customFeeRegistry.FEE_SHARE_DISCOUNT_STEP(), 100, "custom fee share discount step");
        assertEq(customFeeRegistry.MAX_STEPS(), 35, "custom fee max weight steps");
        assertEq(customFeeRegistry.MAX_STEP_VALUE(), 90_000, "custom fee max weight multiplier");
        assertEq(
            customFeeRegistry.MAX_FEE_SHARE_DISCOUNT_CUT_COOLDOWN(),
            365 days,
            "custom fee max discount cut cooldown"
        );
    }

    function test_roles_onlyFull() public view {
        _checkAdminRole(address(customFeeRegistry), deployParams.aragonAgent, deployParams.secondAdminAddress);
    }

    function test_initialization_onlyFull() public {
        _assertNotReinitializable(
            address(customFeeRegistry),
            address(customFeeRegistryImpl),
            abi.encodeCall(
                customFeeRegistry.initialize,
                (deployParams.aragonAgent, 15 days, deployParams.customFeeRegistryConfig.boostSteps)
            )
        );
    }

    function test_proxy_onlyFull() public view {
        _assertProxy(address(customFeeRegistry), address(customFeeRegistryImpl), deployParams.proxyAdmin, "custom fee");
    }
}

contract CuratedGatesDeploymentTest is DeploymentBaseTest {
    function _expectedCurveId(uint256 gateIndex) internal view returns (uint256 curveId) {
        uint256 nextCustomCurveId = 1;
        uint256 gatesCount = deployParams.curatedGates.length;

        for (uint256 i = 0; i < gatesCount; ++i) {
            bool hasCustomCurve = deployParams.curatedGates[i].bondCurve.length != 0;
            uint256 currentCurveId = hasCustomCurve ? nextCustomCurveId : accounting.DEFAULT_BOND_CURVE_ID();
            if (i == gateIndex) return currentCurveId;
            if (hasCustomCurve) ++nextCustomCurveId;
        }

        revert("invalid gate index");
    }

    function _assertCreateRoleOrderMatchesConfig() internal view {
        bytes32 role = module.CREATE_NODE_OPERATOR_ROLE();
        address[] memory members = module.getRoleMembers(role);
        assertEq(members.length, deployParams.curatedGates.length, "unexpected create role members count");

        for (uint256 i = 0; i < members.length; ++i) {
            assertEq(members[i], curatedGates[i], "create role order mismatch");

            CuratedGate gate = CuratedGate(members[i]);
            CuratedGateConfig storage cfg = deployParams.curatedGates[i];
            assertEq(gate.treeRoot(), cfg.treeRoot, "unexpected gate root");
            assertEq(gate.treeCid(), cfg.treeCid, "unexpected gate cid");
            assertEq(gate.name(), cfg.name, "unexpected gate name");
            assertEq(gate.curveId(), _expectedCurveId(i), "unexpected gate curve");
        }
    }

    function test_immutables() public view {
        uint256 gatesCount = curatedGates.length;
        assertGt(gatesCount, 0, "no curated gates deployed");

        for (uint256 i = 0; i < gatesCount; ++i) {
            CuratedGate gate = CuratedGate(curatedGates[i]);

            assertEq(address(gate.MODULE()), address(module));
            assertEq(address(gate.ACCOUNTING()), address(accounting));
            assertEq(address(gate.META_REGISTRY()), address(metaRegistry));
        }
    }

    function test_state() public view {
        uint256 gatesCount = curatedGates.length;
        for (uint256 i = 0; i < gatesCount; ++i) {
            CuratedGate gate = CuratedGate(curatedGates[i]);
            assertEq(gate.getInitializedVersion(), 1);
            assertFalse(gate.isPaused());

            assertEq(gate.treeRoot(), deployParams.curatedGates[i].treeRoot);
            assertEq(gate.treeCid(), deployParams.curatedGates[i].treeCid);
            assertEq(gate.name(), deployParams.curatedGates[i].name);
            assertEq(gate.curveId(), _expectedCurveId(i));
        }
    }

    function test_curveParameters() public view {
        uint256 gatesCount = curatedGates.length;
        assertGt(gatesCount, 0, "no curated gates deployed");

        uint256 expectedCurvesCount = 1;
        _assertBondCurve(accounting, accounting.DEFAULT_BOND_CURVE_ID(), deployParams.defaultBondCurve);

        for (uint256 i = 0; i < gatesCount; ++i) {
            CuratedGate gate = CuratedGate(curatedGates[i]);
            uint256 curveId = gate.curveId();
            CuratedGateConfig storage gateConfig = deployParams.curatedGates[i];

            if (gateConfig.bondCurve.length != 0) {
                ++expectedCurvesCount;
                _assertBondCurve(accounting, curveId, gateConfig.bondCurve);
            }

            GateCurveParams memory params = gateConfig.params;
            assertEq(parametersRegistry.getKeyRemovalCharge(curveId), deployParams.defaultKeyRemovalCharge);

            if (params.generalDelayedPenaltyAdditionalFine.isValue) {
                assertEq(
                    parametersRegistry.getGeneralDelayedPenaltyAdditionalFine(curveId),
                    params.generalDelayedPenaltyAdditionalFine.value
                );
            } else {
                assertEq(
                    parametersRegistry.getGeneralDelayedPenaltyAdditionalFine(curveId),
                    deployParams.defaultGeneralDelayedPenaltyAdditionalFine
                );
            }

            if (params.keysLimit.isValue) {
                assertEq(parametersRegistry.getKeysLimit(curveId), params.keysLimit.value);
            } else {
                assertEq(parametersRegistry.getKeysLimit(curveId), deployParams.defaultKeysLimit);
            }

            IParametersRegistry.KeyNumberValueInterval[] memory avgPerfLeewayData = parametersRegistry
                .getPerformanceLeewayData(curveId);
            if (params.avgPerfLeewayData.length == 0) {
                assertEq(avgPerfLeewayData.length, 1);
                assertEq(avgPerfLeewayData[0].minKeyNumber, 1);
                assertEq(avgPerfLeewayData[0].value, deployParams.defaultAvgPerfLeewayBP);
            } else {
                assertEq(avgPerfLeewayData.length, params.avgPerfLeewayData.length);
                for (uint256 j = 0; j < avgPerfLeewayData.length; ++j) {
                    assertEq(avgPerfLeewayData[j].minKeyNumber, params.avgPerfLeewayData[j][0]);
                    assertEq(avgPerfLeewayData[j].value, params.avgPerfLeewayData[j][1]);
                }
            }

            IParametersRegistry.KeyNumberValueInterval[] memory rewardShareData = parametersRegistry.getRewardShareData(
                curveId
            );
            if (params.rewardShareData.length == 0) {
                assertEq(rewardShareData.length, 1);
                assertEq(rewardShareData[0].minKeyNumber, 1);
                assertEq(rewardShareData[0].value, deployParams.defaultRewardShareBP);
            } else {
                assertEq(rewardShareData.length, params.rewardShareData.length);
                for (uint256 j = 0; j < rewardShareData.length; ++j) {
                    assertEq(rewardShareData[j].minKeyNumber, params.rewardShareData[j][0]);
                    assertEq(rewardShareData[j].value, params.rewardShareData[j][1]);
                }
            }

            (uint256 strikesLifetime, uint256 strikesThreshold) = parametersRegistry.getStrikesParams(curveId);
            if (params.strikesLifetimeFrames.isValue || params.strikesThreshold.isValue) {
                assertEq(strikesLifetime, params.strikesLifetimeFrames.value);
                assertEq(strikesThreshold, params.strikesThreshold.value);
            } else {
                assertEq(strikesLifetime, deployParams.defaultStrikesLifetimeFrames);
                assertEq(strikesThreshold, deployParams.defaultStrikesThreshold);
            }

            (uint256 queuePriority, uint256 queueMaxDeposits) = parametersRegistry.getQueueConfig(curveId);
            assertEq(queuePriority, deployParams.defaultQueuePriority);
            assertEq(queueMaxDeposits, deployParams.defaultQueueMaxDeposits);

            if (params.badPerformancePenalty.isValue) {
                assertEq(parametersRegistry.getBadPerformancePenalty(curveId), params.badPerformancePenalty.value);
            } else {
                assertEq(
                    parametersRegistry.getBadPerformancePenalty(curveId),
                    deployParams.defaultBadPerformancePenalty
                );
            }

            (uint256 attestationsWeight, uint256 blocksWeight, uint256 syncWeight) = parametersRegistry
                .getPerformanceCoefficients(curveId);
            if (params.attestationsWeight.isValue || params.blocksWeight.isValue || params.syncWeight.isValue) {
                assertEq(attestationsWeight, params.attestationsWeight.value);
                assertEq(blocksWeight, params.blocksWeight.value);
                assertEq(syncWeight, params.syncWeight.value);
            } else {
                assertEq(attestationsWeight, deployParams.defaultAttestationsWeight);
                assertEq(blocksWeight, deployParams.defaultBlocksWeight);
                assertEq(syncWeight, deployParams.defaultSyncWeight);
            }

            if (params.allowedExitDelay.isValue) {
                assertEq(parametersRegistry.getAllowedExitDelay(curveId), params.allowedExitDelay.value);
            } else {
                assertEq(parametersRegistry.getAllowedExitDelay(curveId), deployParams.defaultAllowedExitDelay);
            }

            if (params.exitDelayFee.isValue) {
                assertEq(parametersRegistry.getExitDelayFee(curveId), params.exitDelayFee.value);
            } else {
                assertEq(parametersRegistry.getExitDelayFee(curveId), deployParams.defaultExitDelayFee);
            }

            if (params.maxElWithdrawalRequestFee.isValue) {
                assertEq(
                    parametersRegistry.getMaxElWithdrawalRequestFee(curveId),
                    params.maxElWithdrawalRequestFee.value
                );
            } else {
                assertEq(
                    parametersRegistry.getMaxElWithdrawalRequestFee(curveId),
                    deployParams.defaultMaxElWithdrawalRequestFee
                );
            }

            if (params.metaRegistryBondCurveWeight.isValue) {
                assertEq(metaRegistry.getBondCurveWeight(curveId), params.metaRegistryBondCurveWeight.value);
            }
        }

        assertEq(accounting.getCurvesCount(), expectedCurvesCount, "unexpected total curves count");
    }

    function test_proxy() public view {
        uint256 gatesCount = curatedGates.length;
        address implementation = address(curatedGateImpl);
        assertTrue(implementation != address(0), "factory implementation zero");
        for (uint256 i = 0; i < gatesCount; ++i) {
            _assertProxy(curatedGates[i], implementation, deployParams.proxyAdmin, "curated gate");
        }
    }

    function test_roles() public view {
        uint256 gatesCount = curatedGates.length;
        assertGt(gatesCount, 0, "no curated gates deployed");
        bytes32 setBondCurveRole = accounting.SET_BOND_CURVE_ROLE();
        uint256 defaultCurveId = accounting.DEFAULT_BOND_CURVE_ID();
        uint256 setBondCurveRoleMembers;

        for (uint256 i = 0; i < gatesCount; ++i) {
            {
                CuratedGate gate = CuratedGate(curatedGates[i]);
                _checkAdminRole(address(gate), deployParams.aragonAgent, deployParams.secondAdminAddress);

                // Operational roles
                assertTrue(
                    gate.hasRole(gate.SET_TREE_ROLE(), deployParams.easyTrackEVMScriptExecutor),
                    "missing set tree role"
                );
                assertEq(gate.getRoleMemberCount(gate.SET_TREE_ROLE()), 1, "unexpected set tree role members count");

                assertTrue(gate.hasRole(gate.PAUSE_ROLE(), deployParams.curatedGatePauseManager), "missing pause role");
                assertEq(gate.getRoleMemberCount(gate.PAUSE_ROLE()), 1, "unexpected pause role members count");

                assertEq(gate.getRoleMemberCount(gate.RESUME_ROLE()), 0, "unexpected resume role members count");
                assertEq(gate.getRoleMemberCount(gate.RECOVERER_ROLE()), 0, "unexpected recoverer role members count");

                bool hasCustomCurve = gate.curveId() != defaultCurveId;
                assertEq(
                    accounting.hasRole(setBondCurveRole, address(gate)),
                    hasCustomCurve,
                    "unexpected set bond curve role"
                );
                if (hasCustomCurve) setBondCurveRoleMembers += 1;
            }
        }

        assertEq(accounting.getRoleMemberCount(setBondCurveRole), setBondCurveRoleMembers, "set bond curve roles");
    }

    function test_roleWiringMatchesConfiguredGates_onlyFull() public view {
        _assertCreateRoleOrderMatchesConfig();
        uint256 gatesCount = curatedGates.length;

        bytes32 metaSetterRole = metaRegistry.SET_OPERATOR_INFO_ROLE();
        uint256 metaMembersCount = metaRegistry.getRoleMemberCount(metaSetterRole);
        assertEq(metaMembersCount, gatesCount + 1, "unexpected meta setter role members count");

        uint256 defaultCurveId = accounting.DEFAULT_BOND_CURVE_ID();
        bytes32 setBondCurveRole = accounting.SET_BOND_CURVE_ROLE();
        uint256 expectedSetBondCurveMembers;
        for (uint256 i = 0; i < gatesCount; ++i) {
            address gateAddress = curatedGates[i];
            CuratedGate gate = CuratedGate(gateAddress);

            assertTrue(module.hasRole(module.CREATE_NODE_OPERATOR_ROLE(), gateAddress), "missing create role");
            assertTrue(metaRegistry.hasRole(metaSetterRole, gateAddress), "missing meta setter role");

            bool hasCustomCurve = gate.curveId() != defaultCurveId;
            assertEq(
                accounting.hasRole(setBondCurveRole, gateAddress),
                hasCustomCurve,
                "unexpected set bond curve role"
            );
            if (hasCustomCurve) ++expectedSetBondCurveMembers;
        }

        assertTrue(
            metaRegistry.hasRole(metaSetterRole, deployParams.setOperatorInfoManager),
            "missing setOperatorInfoManager role"
        );
        assertEq(accounting.getRoleMemberCount(setBondCurveRole), expectedSetBondCurveMembers, "set bond curve roles");
    }
}

contract CuratedGateFactoryDeploymentTest is DeploymentBaseTest {
    function test_state() public view {
        assertTrue(address(curatedGateFactory) != address(0), "curated gate factory missing");

        address implementation = address(curatedGateImpl);
        assertTrue(implementation != address(0), "curated gate impl missing");
        assertEq(curatedGateFactory.GATE_IMPL(), implementation, "curated gate factory impl mismatch");
    }
}

contract CircuitBreakerDeploymentTest is DeploymentBaseTest {
    function test_pausables_afterVote() public {
        assertEq(circuitBreaker.getPauser(address(module)), deployParams.circuitBreakerPauser, "module pauser");
        assertEq(circuitBreaker.getPauser(address(accounting)), deployParams.circuitBreakerPauser, "accounting pauser");
        assertEq(circuitBreaker.getPauser(address(oracle)), deployParams.circuitBreakerPauser, "oracle pauser");
        assertEq(circuitBreaker.getPauser(address(verifier)), deployParams.circuitBreakerPauser, "verifier pauser");
        assertEq(circuitBreaker.getPauser(address(ejector)), deployParams.circuitBreakerPauser, "ejector pauser");
    }

    function test_roles() public {
        assertTrue(
            curatedModule.hasRole(curatedModule.PAUSE_ROLE(), address(circuitBreaker)),
            "curated module pause role"
        );
        assertTrue(accounting.hasRole(accounting.PAUSE_ROLE(), address(circuitBreaker)), "accounting pause role");
        assertTrue(oracle.hasRole(oracle.PAUSE_ROLE(), address(circuitBreaker)), "oracle pause role");
        assertTrue(verifier.hasRole(verifier.PAUSE_ROLE(), address(circuitBreaker)), "verifier pause role");
        assertTrue(ejector.hasRole(ejector.PAUSE_ROLE(), address(circuitBreaker)), "ejector pause role");
    }
}
