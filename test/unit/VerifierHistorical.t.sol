// SPDX-FileCopyrightText: 2026 Lido <info@lido.fi>
// SPDX-License-Identifier: GPL-3.0
pragma solidity 0.8.33;

import { Test } from "forge-std/Test.sol";

import { IVerifier } from "src/interfaces/IVerifier.sol";
import { IBaseModule, WithdrawnValidatorInfo } from "src/interfaces/IBaseModule.sol";
import { GIndex } from "src/lib/GIndex.sol";

import { Verifier } from "src/Verifier.sol";
import { Slot } from "src/lib/Types.sol";
import { SSZ } from "src/lib/SSZ.sol";

import { Utilities } from "test/helpers/Utilities.sol";
import { Stub } from "test/helpers/mocks/Stub.sol";

function dec(Slot self) pure returns (Slot slot) {
    assembly ("memory-safe") {
        slot := sub(self, 1)
    }
}

function inc(Slot self) pure returns (Slot slot) {
    assembly ("memory-safe") {
        slot := add(self, 1)
    }
}

using { dec, inc } for Slot;

GIndex constant NULL_GINDEX = GIndex.wrap(0);

contract VerifierHistoricalBase is Test, Utilities {
    struct Fixture {
        bytes32 blockRoot;
        IVerifier.ProcessWithdrawalInput data;
    }

    Fixture public fixture;

    Stub public module;
    Verifier public verifier;

    function _setMocks() internal {
        vm.mockCall(
            verifier.BEACON_ROOTS(),
            abi.encode(fixture.data.recentBlock.rootsTimestamp),
            abi.encode(fixture.blockRoot)
        );

        vm.mockCall(
            address(module),
            abi.encodeWithSelector(IBaseModule.getSigningKeys.selector, 0, 0),
            abi.encode(fixture.data.validator.object.pubkey)
        );

        vm.mockCall(
            address(module),
            abi.encodeWithSelector(
                IBaseModule.getKeyConfirmedBalances.selector,
                fixture.data.validator.nodeOperatorId,
                fixture.data.validator.keyIndex,
                1
            ),
            abi.encode(UintArr(0))
        );

        vm.mockCall(address(module), abi.encodeWithSelector(IBaseModule.reportRegularWithdrawnValidators.selector), "");
    }

    function _loadFixture(string memory fork) internal {
        string[] memory cmd = new string[](4);
        cmd[0] = "node";
        cmd[1] = "--no-warnings";
        cmd[2] = "test/fixtures/Verifier/historical_withdrawal.mjs";
        cmd[3] = fork;
        bytes memory res = vm.ffi(cmd);
        fixture = abi.decode(res, (Fixture));
    }

    function ffi_interface(Fixture memory) external {}
}

contract VerifierWithdrawalHistoricalTest is VerifierHistoricalBase {
    function setUp() public {
        _loadFixture("gloas");

        module = new Stub();
        verifier = new Verifier({
            withdrawalCredentials: fixture.data.validator.object.withdrawalCredentials,
            module: address(module),
            slotsPerEpoch: 32,
            firstSupportedSlot: fixture.data.withdrawalBlock.header.slot,
            gloasSlot: fixture.data.withdrawalBlock.header.slot,
            capellaSlot: Slot.wrap(0),
            minWithdrawalRatio: 9000,
            admin: nextAddress("ADMIN")
        });

        _setMocks();
    }

    function test_processHistoricalWithdrawalProof_HappyPath() public {
        WithdrawnValidatorInfo[] memory withdrawals = new WithdrawnValidatorInfo[](1);
        withdrawals[0] = WithdrawnValidatorInfo({
            nodeOperatorId: 0,
            keyIndex: 0,
            exitBalance: uint256(fixture.data.withdrawal.object.amount) * 1e9,
            slashingPenalty: 0,
            isSlashed: false
        });

        vm.expectCall(
            address(module),
            abi.encodeWithSelector(IBaseModule.reportRegularWithdrawnValidators.selector, withdrawals)
        );

        verifier.processHistoricalWithdrawalProof(fixture.data);
    }

    function test_processHistoricalWithdrawalProof_RevertWhen_UnsupportedSlot_WithdrawalBlock() public {
        fixture.data.withdrawalBlock.header.slot = verifier.FIRST_SUPPORTED_SLOT().dec();

        vm.expectRevert(
            abi.encodeWithSelector(IVerifier.UnsupportedSlot.selector, fixture.data.withdrawalBlock.header.slot)
        );

        verifier.processHistoricalWithdrawalProof(fixture.data);
    }

    function test_processHistoricalWithdrawalProof_RevertWhen_UnsupportedSlot_RecentBlock() public {
        fixture.data.recentBlock.header.slot = verifier.FIRST_SUPPORTED_SLOT().dec();

        vm.expectRevert(
            abi.encodeWithSelector(IVerifier.UnsupportedSlot.selector, fixture.data.recentBlock.header.slot)
        );

        verifier.processHistoricalWithdrawalProof(fixture.data);
    }

    function test_processHistoricalWithdrawalProof_RevertWhen_InvalidRecentBlock() public {
        vm.mockCall(
            verifier.BEACON_ROOTS(),
            abi.encode(fixture.data.recentBlock.rootsTimestamp),
            abi.encode(hex"deadbeef")
        );

        vm.expectRevert(IVerifier.InvalidBlockHeader.selector);
        verifier.processHistoricalWithdrawalProof(fixture.data);
    }

    function test_processHistoricalWithdrawalProof_RevertWhen_InvalidWithdrawalBlock() public {
        // Breaking something in the header.
        fixture.data.withdrawalBlock.header.parentRoot = someBytes32();

        vm.expectRevert(SSZ.InvalidProof.selector);
        verifier.processHistoricalWithdrawalProof(fixture.data);
    }

    function test_processHistoricalWithdrawalProof_RevertWhen_InvalidPublicKey() public {
        vm.mockCall(
            address(module),
            abi.encodeWithSelector(
                IBaseModule.getSigningKeys.selector,
                fixture.data.validator.nodeOperatorId,
                fixture.data.validator.keyIndex
            ),
            abi.encode(hex"deadbeef")
        );

        vm.expectRevert(IVerifier.InvalidPublicKey.selector);
        verifier.processHistoricalWithdrawalProof(fixture.data);
    }

    function test_processHistoricalWithdrawalProof_RevertWhen_InvalidWithdrawalCredentials() public {
        fixture.data.validator.object.withdrawalCredentials = someBytes32();

        vm.expectRevert(IVerifier.InvalidWithdrawalCredentials.selector);
        verifier.processHistoricalWithdrawalProof(fixture.data);
    }

    function test_processHistoricalWithdrawalProof_RevertWhen_InvalidWithdrawalAddress() public {
        fixture.data.withdrawal.object.withdrawalAddress = nextAddress();

        vm.expectRevert(IVerifier.InvalidWithdrawalAddress.selector);
        verifier.processHistoricalWithdrawalProof(fixture.data);
    }

    function test_processHistoricalWithdrawalProof_RevertWhen_ValidatorIsNotWithdrawable() public {
        fixture.data.validator.object.withdrawableEpoch = fixture.data.recentBlock.header.slot.unwrap() / 32 + 1;

        vm.expectRevert(IVerifier.ValidatorIsNotWithdrawable.selector);
        verifier.processHistoricalWithdrawalProof(fixture.data);
    }

    function test_processHistoricalWithdrawalProof_RevertWhen_ValidatorSlashed() public {
        fixture.data.validator.object.slashed = true;

        vm.expectRevert(IVerifier.ValidatorIsSlashed.selector);
        verifier.processHistoricalWithdrawalProof(fixture.data);
    }

    function test_processHistoricalWithdrawalProof_RevertWhen_ValidatorIndexDoesNotMatch() public {
        fixture.data.withdrawal.object.validatorIndex = fixture.data.validator.index + 1;

        vm.expectRevert(IVerifier.InvalidValidatorIndex.selector);
        verifier.processHistoricalWithdrawalProof(fixture.data);
    }

    function test_processHistoricalWithdrawalProof_RevertWhen_PartialWithdrawal() public {
        // 32 ether in gwei * 9000 / 10000 = 28_800_000_000 gwei = 28.8 ether
        fixture.data.withdrawal.object.amount = 28_800_000_000 - 1;

        vm.expectRevert(IVerifier.PartialWithdrawal.selector);
        verifier.processHistoricalWithdrawalProof(fixture.data);
    }
}

contract VerifierWithdrawalCrossForkHistoricalTest is VerifierHistoricalBase {
    function setUp() public virtual {
        module = new Stub();
    }

    function test_processHistoricalWithdrawalProof_AfterGloas() public {
        _loadFixture("electra");

        verifier = new Verifier({
            withdrawalCredentials: fixture.data.validator.object.withdrawalCredentials,
            module: address(module),
            slotsPerEpoch: 32,
            firstSupportedSlot: fixture.data.withdrawalBlock.header.slot,
            gloasSlot: fixture.data.recentBlock.header.slot.dec(),
            capellaSlot: Slot.wrap(0),
            minWithdrawalRatio: 9000,
            admin: nextAddress("ADMIN")
        });

        _setMocks();

        WithdrawnValidatorInfo[] memory withdrawals = new WithdrawnValidatorInfo[](1);
        withdrawals[0] = WithdrawnValidatorInfo({
            nodeOperatorId: 0,
            keyIndex: 0,
            exitBalance: uint256(fixture.data.withdrawal.object.amount) * 1e9,
            slashingPenalty: 0,
            isSlashed: false
        });

        vm.expectCall(
            address(module),
            abi.encodeWithSelector(IBaseModule.reportRegularWithdrawnValidators.selector, withdrawals)
        );

        verifier.processHistoricalWithdrawalProof(fixture.data);
    }

    function test_processHistoricalWithdrawalProof_AtGloas() public {
        _loadFixture("electra");

        verifier = new Verifier({
            withdrawalCredentials: fixture.data.validator.object.withdrawalCredentials,
            module: address(module),
            slotsPerEpoch: 32,
            firstSupportedSlot: fixture.data.withdrawalBlock.header.slot,
            gloasSlot: fixture.data.recentBlock.header.slot,
            capellaSlot: Slot.wrap(0),
            minWithdrawalRatio: 9000,
            admin: nextAddress("ADMIN")
        });

        _setMocks();

        WithdrawnValidatorInfo[] memory withdrawals = new WithdrawnValidatorInfo[](1);
        withdrawals[0] = WithdrawnValidatorInfo({
            nodeOperatorId: 0,
            keyIndex: 0,
            exitBalance: uint256(fixture.data.withdrawal.object.amount) * 1e9,
            slashingPenalty: 0,
            isSlashed: false
        });

        vm.expectCall(
            address(module),
            abi.encodeWithSelector(IBaseModule.reportRegularWithdrawnValidators.selector, withdrawals)
        );

        verifier.processHistoricalWithdrawalProof(fixture.data);
    }
}

contract VerifierCrossForkHistoricalBalanceTest is Test, Utilities {
    struct Fixture {
        bytes32 blockRoot;
        IVerifier.ProcessHistoricalBalanceProofInput data;
    }

    Fixture public fixture;

    Stub public module;
    Verifier public verifier;
    address public admin;

    function setUp() public {
        _loadFixture("electra");

        module = new Stub();
        admin = nextAddress("ADMIN");

        verifier = new Verifier({
            withdrawalCredentials: someBytes32(),
            module: address(module),
            slotsPerEpoch: 32,
            firstSupportedSlot: fixture.data.historicalBlock.header.slot,
            gloasSlot: fixture.data.recentBlock.header.slot.dec(),
            capellaSlot: Slot.wrap(0),
            minWithdrawalRatio: 9000,
            admin: admin
        });

        vm.startPrank(admin);
        verifier.grantRole(verifier.PAUSE_ROLE(), admin);
        verifier.grantRole(verifier.RESUME_ROLE(), admin);
        vm.stopPrank();

        _setMocks();
    }

    function test_processHistoricalBalanceProof_HappyPath() public {
        vm.expectCall(address(module), abi.encodeWithSelector(IBaseModule.reportValidatorBalance.selector));

        verifier.processHistoricalBalanceProof(fixture.data);
    }

    function _setMocks() internal {
        vm.mockCall(
            verifier.BEACON_ROOTS(),
            abi.encode(fixture.data.recentBlock.rootsTimestamp),
            abi.encode(fixture.blockRoot)
        );

        vm.mockCall(
            address(module),
            abi.encodeWithSelector(IBaseModule.getSigningKeys.selector, 0, 0),
            abi.encode(fixture.data.validator.object.pubkey)
        );

        vm.mockCall(address(module), abi.encodeWithSelector(IBaseModule.reportValidatorBalance.selector), "");
    }

    function _loadFixture(string memory fork) internal {
        string[] memory cmd = new string[](4);
        cmd[0] = "node";
        cmd[1] = "--no-warnings";
        cmd[2] = "test/fixtures/Verifier/historical_balance.mjs";
        cmd[3] = fork;
        bytes memory res = vm.ffi(cmd);
        fixture = abi.decode(res, (Fixture));
    }

    function ffi_interface(Fixture memory) external {}
}

contract VerifierCrossForkHistoricalBalanceAtGloasSlotTest is Test, Utilities {
    struct Fixture {
        bytes32 blockRoot;
        IVerifier.ProcessHistoricalBalanceProofInput data;
    }

    Fixture public fixture;

    Stub public module;
    Verifier public verifier;
    address public admin;

    function setUp() public {
        _loadFixture("electra");

        module = new Stub();
        admin = nextAddress("ADMIN");

        verifier = new Verifier({
            withdrawalCredentials: someBytes32(),
            module: address(module),
            slotsPerEpoch: 32,
            firstSupportedSlot: fixture.data.historicalBlock.header.slot,
            gloasSlot: fixture.data.recentBlock.header.slot,
            capellaSlot: Slot.wrap(0),
            minWithdrawalRatio: 9000,
            admin: admin
        });

        vm.startPrank(admin);
        verifier.grantRole(verifier.PAUSE_ROLE(), admin);
        verifier.grantRole(verifier.RESUME_ROLE(), admin);
        vm.stopPrank();

        _setMocks();
    }

    function test_processHistoricalBalanceProof_HappyPath() public {
        vm.expectCall(address(module), abi.encodeWithSelector(IBaseModule.reportValidatorBalance.selector));

        verifier.processHistoricalBalanceProof(fixture.data);
    }

    function _setMocks() internal {
        vm.mockCall(
            verifier.BEACON_ROOTS(),
            abi.encode(fixture.data.recentBlock.rootsTimestamp),
            abi.encode(fixture.blockRoot)
        );

        vm.mockCall(
            address(module),
            abi.encodeWithSelector(IBaseModule.getSigningKeys.selector, 0, 0),
            abi.encode(fixture.data.validator.object.pubkey)
        );

        vm.mockCall(address(module), abi.encodeWithSelector(IBaseModule.reportValidatorBalance.selector), "");
    }

    function _loadFixture(string memory fork) internal {
        string[] memory cmd = new string[](4);
        cmd[0] = "node";
        cmd[1] = "--no-warnings";
        cmd[2] = "test/fixtures/Verifier/historical_balance.mjs";
        cmd[3] = fork;
        bytes memory res = vm.ffi(cmd);
        fixture = abi.decode(res, (Fixture));
    }

    function ffi_interface(Fixture memory) external {}
}

contract VerifierHistoricalBalanceTest is Test, Utilities {
    struct Fixture {
        bytes32 blockRoot;
        IVerifier.ProcessHistoricalBalanceProofInput data;
    }

    Fixture public fixture;

    Stub public module;
    Verifier public verifier;
    address public admin;

    function setUp() public {
        _loadFixture("gloas");

        module = new Stub();
        admin = nextAddress("ADMIN");

        verifier = new Verifier({
            withdrawalCredentials: someBytes32(),
            module: address(module),
            slotsPerEpoch: 32,
            firstSupportedSlot: Slot.wrap(8192),
            gloasSlot: Slot.wrap(8192),
            capellaSlot: Slot.wrap(0),
            minWithdrawalRatio: 9000,
            admin: admin
        });

        vm.startPrank(admin);
        verifier.grantRole(verifier.PAUSE_ROLE(), admin);
        verifier.grantRole(verifier.RESUME_ROLE(), admin);
        vm.stopPrank();

        _setMocks();
    }

    function test_processHistoricalBalanceProof_HappyPath() public {
        vm.expectCall(address(module), abi.encodeWithSelector(IBaseModule.reportValidatorBalance.selector));

        verifier.processHistoricalBalanceProof(fixture.data);
    }

    function test_processHistoricalBalanceProof_RevertWhen_UnsupportedSlot_RecentBlock() public {
        fixture.data.recentBlock.header.slot = verifier.FIRST_SUPPORTED_SLOT().dec();

        vm.expectRevert(
            abi.encodeWithSelector(IVerifier.UnsupportedSlot.selector, fixture.data.recentBlock.header.slot)
        );

        verifier.processHistoricalBalanceProof(fixture.data);
    }

    function test_processHistoricalBalanceProof_RevertWhen_UnsupportedSlot_HistoricalBlock() public {
        fixture.data.historicalBlock.header.slot = verifier.FIRST_SUPPORTED_SLOT().dec();

        vm.expectRevert(
            abi.encodeWithSelector(IVerifier.UnsupportedSlot.selector, fixture.data.historicalBlock.header.slot)
        );

        verifier.processHistoricalBalanceProof(fixture.data);
    }

    function test_processHistoricalBalanceProof_RevertWhen_InvalidRecentBlock() public {
        vm.mockCall(
            verifier.BEACON_ROOTS(),
            abi.encode(fixture.data.recentBlock.rootsTimestamp),
            abi.encode(hex"deadbeef")
        );

        vm.expectRevert(IVerifier.InvalidBlockHeader.selector);
        verifier.processHistoricalBalanceProof(fixture.data);
    }

    function test_processHistoricalBalanceProof_RevertWhen_ValidatorIsWithdrawable() public {
        fixture.data.validator.object.withdrawableEpoch = uint64(
            fixture.data.historicalBlock.header.slot.unwrap() / 32
        );

        vm.expectRevert(IVerifier.ValidatorIsWithdrawable.selector);
        verifier.processHistoricalBalanceProof(fixture.data);
    }

    function test_processHistoricalBalanceProof_RevertWhen_InvalidHistoricalBlock() public {
        fixture.data.historicalBlock.header.parentRoot = someBytes32();

        vm.expectRevert(SSZ.InvalidProof.selector);
        verifier.processHistoricalBalanceProof(fixture.data);
    }

    function test_processHistoricalBalanceProof_RevertWhen_InvalidBalanceNode() public {
        fixture.data.balance.node = someBytes32();

        vm.expectRevert(SSZ.InvalidProof.selector);
        verifier.processHistoricalBalanceProof(fixture.data);
    }

    function test_processHistoricalBalanceProof_RevertWhen_InvalidPublicKey() public {
        vm.mockCall(
            address(module),
            abi.encodeWithSelector(
                IBaseModule.getSigningKeys.selector,
                fixture.data.validator.nodeOperatorId,
                fixture.data.validator.keyIndex
            ),
            abi.encode(hex"deadbeef")
        );

        vm.expectRevert(IVerifier.InvalidPublicKey.selector);
        verifier.processHistoricalBalanceProof(fixture.data);
    }

    function _setMocks() internal {
        vm.mockCall(
            verifier.BEACON_ROOTS(),
            abi.encode(fixture.data.recentBlock.rootsTimestamp),
            abi.encode(fixture.blockRoot)
        );

        vm.mockCall(
            address(module),
            abi.encodeWithSelector(IBaseModule.getSigningKeys.selector, 0, 0),
            abi.encode(fixture.data.validator.object.pubkey)
        );

        vm.mockCall(address(module), abi.encodeWithSelector(IBaseModule.reportValidatorBalance.selector), "");
    }

    function _loadFixture(string memory fork) internal {
        string[] memory cmd = new string[](4);
        cmd[0] = "node";
        cmd[1] = "--no-warnings";
        cmd[2] = "test/fixtures/Verifier/historical_balance.mjs";
        cmd[3] = fork;
        bytes memory res = vm.ffi(cmd);
        fixture = abi.decode(res, (Fixture));
    }

    function ffi_interface(Fixture memory) external {}
}
