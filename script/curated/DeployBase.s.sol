// SPDX-FileCopyrightText: 2026 Lido <info@lido.fi>
// SPDX-License-Identifier: GPL-3.0

pragma solidity 0.8.33;

import { Script } from "forge-std/Script.sol";

import { UpgradeableBeacon } from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";

import { HashConsensus } from "../../src/lib/base-oracle/HashConsensus.sol";
import { OssifiableProxy } from "../../src/lib/proxy/OssifiableProxy.sol";
import { CuratedModule } from "../../src/CuratedModule.sol";
import { Accounting } from "../../src/Accounting.sol";
import { FeeDistributor } from "../../src/FeeDistributor.sol";
import { Ejector } from "../../src/Ejector.sol";
import { ValidatorStrikes } from "../../src/ValidatorStrikes.sol";
import { FeeOracle } from "../../src/FeeOracle.sol";
import { Verifier } from "../../src/Verifier.sol";
import { ParametersRegistry } from "../../src/ParametersRegistry.sol";
import { ExitPenalties } from "../../src/ExitPenalties.sol";
import { MetaRegistry } from "../../src/MetaRegistry.sol";
import { AdditionalBondRegistry } from "../../src/AdditionalBondRegistry.sol";
import { NodeOperatorStrikes } from "../../src/NodeOperatorStrikes.sol";
import { CustomFeeRegistry } from "../../src/CustomFeeRegistry.sol";
import { ERC20LockBoostProvider } from "../../src/ERC20LockBoostProvider.sol";
import { LidoGovernanceLockVault } from "../../src/LidoGovernanceLockVault.sol";
import { Step } from "../../src/interfaces/IStepwiseWeightBoost.sol";
import { CuratedGate } from "../../src/CuratedGate.sol";
import { MerkleGateFactory } from "../../src/MerkleGateFactory.sol";

import { ILidoLocator } from "../../src/interfaces/ILidoLocator.sol";
import { ICircuitBreaker } from "../../src/interfaces/ICircuitBreaker.sol";
import { BaseOracle } from "../../src/lib/base-oracle/BaseOracle.sol";
import { IVerifier } from "../../src/interfaces/IVerifier.sol";
import { IParametersRegistry } from "../../src/interfaces/IParametersRegistry.sol";
import { IBondCurve } from "../../src/interfaces/IBondCurve.sol";
import { IMetaRegistry } from "../../src/interfaces/IMetaRegistry.sol";
import { IWeightBoostProvider } from "../../src/interfaces/IWeightBoostProvider.sol";

import { JsonObj, Json } from "../utils/Json.sol";
import { Dummy } from "../utils/Dummy.sol";
import { CommonScriptUtils } from "../utils/Common.sol";
import { GIndex } from "../../src/lib/GIndex.sol";
import { Slot } from "../../src/lib/Types.sol";

struct GateCurveParams {
    IParametersRegistry.MarkedUint248 generalDelayedPenaltyAdditionalFine;
    IParametersRegistry.MarkedUint248 keysLimit;
    uint256[2][] avgPerfLeewayData;
    // Legacy ParametersRegistry compatibility value. Curated fees are sourced from CustomFeeRegistry.
    uint256[2][] rewardShareData;
    IParametersRegistry.MarkedUint248 strikesLifetimeFrames;
    IParametersRegistry.MarkedUint248 strikesThreshold;
    IParametersRegistry.MarkedUint248 badPerformancePenalty;
    IParametersRegistry.MarkedUint248 attestationsWeight;
    IParametersRegistry.MarkedUint248 blocksWeight;
    IParametersRegistry.MarkedUint248 syncWeight;
    IParametersRegistry.MarkedUint248 metaRegistryBondCurveWeight;
    IParametersRegistry.MarkedUint248 allowedExitDelay;
    IParametersRegistry.MarkedUint248 exitDelayFee;
    IParametersRegistry.MarkedUint248 maxElWithdrawalRequestFee;
}

struct CuratedGateConfig {
    string name;
    uint256[2][] bondCurve;
    bytes32 treeRoot;
    string treeCid;
    GateCurveParams params;
}

struct AdditionalBondRegistryConfig {
    uint256 curveMultiplierReductionCooldown;
    // `threshold` is a curve multiplier increment and `value` a weight multiplier increment, both above MAX_BP.
    Step[] boostSteps;
}

struct NodeOperatorStrikesConfig {
    address committee;
    // `threshold` is the minimum active strike count and `value` the weight reduction from MAX_BP.
    Step[] thresholds;
}

struct ERC20LockBoostProviderConfig {
    address token;
    address votingContract;
    address snapshotDelegation;
    uint256 minLockPeriod;
    uint256 lockPeriod;
    Step[] lockBoostSteps;
}

struct CustomFeeRegistryConfig {
    uint256 feeShareDiscountCutCooldown;
    Step[] boostSteps;
}

struct CuratedDeployParams {
    // Lido addresses
    address lidoLocatorAddress;
    address aragonAgent;
    address easyTrackEVMScriptExecutor;
    address proxyAdmin;
    // Oracle
    uint256 secondsPerSlot;
    uint256 slotsPerEpoch;
    uint256 clGenesisTime;
    uint256 oracleReportEpochsPerFrame;
    uint256 fastLaneLengthSlots;
    uint256 consensusVersion;
    address[] oracleMembers;
    uint256 hashConsensusQuorum;
    // Verifier
    GIndex gIFirstWithdrawal;
    GIndex gIFirstValidator;
    GIndex gIFirstHistoricalSummary;
    GIndex gIFirstBalanceNode;
    uint256 verifierFirstSupportedSlot;
    uint256 capellaSlot;
    uint256 minWithdrawalRatio;
    // Accounting
    uint256[2][] defaultBondCurve;
    uint256 minBondLockPeriod;
    uint256 maxBondLockPeriod;
    uint256 bondLockPeriod;
    address chargePenaltyRecipient;
    // Module
    bytes32 moduleType;
    address generalDelayedPenaltyReporter;
    // ParametersRegistry
    uint256 queueLowestPriority;
    uint256 defaultKeyRemovalCharge;
    uint256 defaultGeneralDelayedPenaltyAdditionalFine;
    uint256 defaultKeysLimit;
    uint256 defaultAvgPerfLeewayBP;
    // Legacy ParametersRegistry compatibility value. Curated fees are sourced from CustomFeeRegistry.
    uint256 defaultRewardShareBP;
    uint256 defaultStrikesLifetimeFrames;
    uint256 defaultStrikesThreshold;
    uint256 defaultQueuePriority;
    uint256 defaultQueueMaxDeposits;
    uint256 defaultBadPerformancePenalty;
    uint256 defaultAttestationsWeight;
    uint256 defaultBlocksWeight;
    uint256 defaultSyncWeight;
    uint256 defaultAllowedExitDelay;
    uint256 defaultExitDelayFee;
    uint256 defaultMaxElWithdrawalRequestFee;
    address penaltiesManager;
    // Curated gates
    CuratedGateConfig[] curatedGates;
    address curatedGatePauseManager;
    // MetaRegistry
    address setOperatorInfoManager;
    // CircuitBreaker
    address circuitBreaker;
    address circuitBreakerPauser;
    // DG
    address resealManager;
    // Testnet stuff
    address secondAdminAddress;
    // AdditionalBondRegistry
    AdditionalBondRegistryConfig additionalBondRegistryConfig;
    // NodeOperatorStrikes
    NodeOperatorStrikesConfig nodeOperatorStrikesConfig;
    // LDO lock boost provider
    ERC20LockBoostProviderConfig ldoLockBoostProviderConfig;
    // CustomFeeRegistry
    CustomFeeRegistryConfig customFeeRegistryConfig;
}

abstract contract DeployBase is Script {
    string internal gitRef;
    CuratedDeployParams internal config;
    string internal artifactDir;
    string internal chainName;
    uint256 internal chainId;
    ILidoLocator internal locator;

    address internal deployer;
    CuratedModule public curatedModule;
    Accounting public accounting;
    FeeOracle public oracle;
    FeeDistributor public feeDistributor;
    ExitPenalties public exitPenalties;
    Ejector public ejector;
    ValidatorStrikes public strikes;
    Verifier public verifier;
    HashConsensus public hashConsensus;
    ParametersRegistry public parametersRegistry;
    MetaRegistry public metaRegistry;
    AdditionalBondRegistry public additionalBondRegistry;
    NodeOperatorStrikes public nodeOperatorStrikes;
    ERC20LockBoostProvider public ldoLockBoostProvider;
    ERC20LockBoostProvider public ldoLockBoostProviderImpl;
    LidoGovernanceLockVault public ldoLockVaultImpl;
    UpgradeableBeacon public ldoLockVaultBeacon;
    CustomFeeRegistry public customFeeRegistry;
    CustomFeeRegistry public customFeeRegistryImpl;
    MerkleGateFactory public curatedGateFactory;
    address[] public curatedGateInstances;
    address internal curatedGateImpl;
    address public circuitBreaker;

    error ChainIdMismatch(uint256 actual, uint256 expected);
    error HashConsensusMismatch();
    error CannotBeUsedInMainnet();
    error InvalidSecondAdmin();
    error InvalidInput(string reason);

    function _m(uint256 v) internal pure returns (IParametersRegistry.MarkedUint248 memory) {
        return IParametersRegistry.MarkedUint248({ value: uint248(v), isValue: true });
    }

    constructor(string memory _chainName, uint256 _chainId) {
        chainName = _chainName;
        chainId = _chainId;
    }

    function _setUp() internal {
        vm.label(config.aragonAgent, "ARAGON_AGENT_ADDRESS");
        vm.label(config.lidoLocatorAddress, "LIDO_LOCATOR");
        vm.label(config.easyTrackEVMScriptExecutor, "EVM_SCRIPT_EXECUTOR");
        locator = ILidoLocator(config.lidoLocatorAddress);
        circuitBreaker = config.circuitBreaker;
    }

    function run(string memory _gitRef) external virtual {
        gitRef = _gitRef;
        if (chainId != block.chainid) revert ChainIdMismatch({ actual: block.chainid, expected: chainId });
        HashConsensus accountingConsensus = HashConsensus(
            BaseOracle(locator.accountingOracle()).getConsensusContract()
        );
        (address[] memory members, ) = accountingConsensus.getMembers();
        uint256 quorum = accountingConsensus.getQuorum();
        if (block.chainid == 1) {
            if (
                keccak256(abi.encode(config.oracleMembers)) != keccak256(abi.encode(members)) ||
                config.hashConsensusQuorum != quorum
            ) {
                revert HashConsensusMismatch();
            }
        }
        artifactDir = vm.envOr("ARTIFACTS_DIR", string("./artifacts/local/"));

        vm.startBroadcast();
        (, deployer, ) = vm.readCallers();
        vm.label(deployer, "DEPLOYER");
        uint256 gatesCount = config.curatedGates.length;
        uint256[] memory curatedCurveIds = new uint256[](gatesCount);

        {
            ParametersRegistry parametersRegistryImpl = new ParametersRegistry(config.queueLowestPriority);
            IParametersRegistry.InitializationData memory parametersRegistryData = IParametersRegistry
                .InitializationData({
                    defaultKeyRemovalCharge: config.defaultKeyRemovalCharge,
                    defaultGeneralDelayedPenaltyAdditionalFine: config.defaultGeneralDelayedPenaltyAdditionalFine,
                    defaultKeysLimit: config.defaultKeysLimit,
                    defaultRewardShare: config.defaultRewardShareBP,
                    defaultPerformanceLeeway: config.defaultAvgPerfLeewayBP,
                    defaultStrikesLifetime: config.defaultStrikesLifetimeFrames,
                    defaultStrikesThreshold: config.defaultStrikesThreshold,
                    defaultQueuePriority: config.defaultQueuePriority,
                    defaultQueueMaxDeposits: config.defaultQueueMaxDeposits,
                    defaultBadPerformancePenalty: config.defaultBadPerformancePenalty,
                    defaultAttestationsWeight: config.defaultAttestationsWeight,
                    defaultBlocksWeight: config.defaultBlocksWeight,
                    defaultSyncWeight: config.defaultSyncWeight,
                    defaultAllowedExitDelay: config.defaultAllowedExitDelay,
                    defaultExitDelayFee: config.defaultExitDelayFee,
                    defaultMaxElWithdrawalRequestFee: config.defaultMaxElWithdrawalRequestFee
                });
            parametersRegistry = ParametersRegistry(
                _deployProxy(
                    config.proxyAdmin,
                    address(parametersRegistryImpl),
                    abi.encodeCall(ParametersRegistry.initialize, (deployer, parametersRegistryData))
                )
            );

            Dummy dummyImpl = new Dummy();

            curatedModule = CuratedModule(_deployProxy(deployer, address(dummyImpl)));

            accounting = Accounting(_deployProxy(deployer, address(dummyImpl)));
            oracle = FeeOracle(_deployProxy(deployer, address(dummyImpl)));
            metaRegistry = MetaRegistry(_deployProxy(deployer, address(dummyImpl)));
            additionalBondRegistry = AdditionalBondRegistry(_deployProxy(deployer, address(dummyImpl)));
            nodeOperatorStrikes = NodeOperatorStrikes(_deployProxy(deployer, address(dummyImpl)));
            ldoLockBoostProvider = ERC20LockBoostProvider(_deployProxy(deployer, address(dummyImpl)));
            customFeeRegistry = CustomFeeRegistry(_deployProxy(deployer, address(dummyImpl)));

            FeeDistributor feeDistributorImpl = new FeeDistributor({
                stETH: locator.lido(),
                accounting: address(accounting),
                oracle: address(oracle)
            });
            feeDistributor = FeeDistributor(
                _deployProxy(
                    config.proxyAdmin,
                    address(feeDistributorImpl),
                    abi.encodeCall(FeeDistributor.initialize, (deployer, config.aragonAgent))
                )
            );

            // prettier-ignore
            verifier = new Verifier({
                withdrawalAddress: locator.withdrawalVault(),
                module: address(curatedModule),
                slotsPerEpoch: uint64(config.slotsPerEpoch),
                gindices: IVerifier.GIndices({
                    gIFirstWithdrawalPrev: config.gIFirstWithdrawal,
                    gIFirstWithdrawalCurr: config.gIFirstWithdrawal,
                    gIFirstValidatorPrev: config.gIFirstValidator,
                    gIFirstValidatorCurr: config.gIFirstValidator,
                    gIFirstHistoricalSummaryPrev: config.gIFirstHistoricalSummary,
                    gIFirstHistoricalSummaryCurr: config.gIFirstHistoricalSummary,
                    gIFirstBalanceNodePrev: config.gIFirstBalanceNode,
                    gIFirstBalanceNodeCurr: config.gIFirstBalanceNode
                }),
                firstSupportedSlot: Slot.wrap(uint64(config.verifierFirstSupportedSlot)),
                pivotSlot: Slot.wrap(uint64(config.verifierFirstSupportedSlot)),
                capellaSlot: Slot.wrap(uint64(config.capellaSlot)),
                minWithdrawalRatio: config.minWithdrawalRatio,
                admin: deployer
            });

            Accounting accountingImpl = new Accounting({
                lidoLocator: config.lidoLocatorAddress,
                module: address(curatedModule),
                feeDistributor: address(feeDistributor),
                minBondLockPeriod: config.minBondLockPeriod,
                maxBondLockPeriod: config.maxBondLockPeriod
            });

            IBondCurve.BondCurveIntervalInput[] memory defaultBondCurve = CommonScriptUtils
                .arraysToBondCurveIntervalsInputs(config.defaultBondCurve);
            {
                OssifiableProxy accountingProxy = OssifiableProxy(payable(address(accounting)));
                accountingProxy.proxy__upgradeToAndCall(
                    address(accountingImpl),
                    abi.encodeCall(
                        Accounting.initialize,
                        (defaultBondCurve, deployer, config.bondLockPeriod, config.chargePenaltyRecipient)
                    )
                );
                accountingProxy.proxy__changeAdmin(config.proxyAdmin);
            }

            exitPenalties = ExitPenalties(_deployProxy(deployer, address(dummyImpl)));

            CuratedModule curatedModuleImpl = new CuratedModule({
                moduleType: config.moduleType,
                lidoLocator: config.lidoLocatorAddress,
                parametersRegistry: address(parametersRegistry),
                accounting: address(accounting),
                exitPenalties: address(exitPenalties),
                metaRegistry: address(metaRegistry)
            });

            _upgradeAndHandoffProxy(
                address(curatedModule),
                address(curatedModuleImpl),
                abi.encodeCall(CuratedModule.initialize, (deployer))
            );

            MetaRegistry metaRegistryImpl = new MetaRegistry({ module: address(curatedModule) });

            _upgradeAndHandoffProxy(
                address(metaRegistry),
                address(metaRegistryImpl),
                abi.encodeCall(MetaRegistry.initialize, (deployer))
            );

            AdditionalBondRegistry additionalBondRegistryImpl = new AdditionalBondRegistry({
                module: address(curatedModule)
            });

            _upgradeAndHandoffProxy(
                address(additionalBondRegistry),
                address(additionalBondRegistryImpl),
                abi.encodeCall(
                    AdditionalBondRegistry.initialize,
                    (
                        deployer,
                        config.additionalBondRegistryConfig.curveMultiplierReductionCooldown,
                        config.additionalBondRegistryConfig.boostSteps
                    )
                )
            );

            NodeOperatorStrikes nodeOperatorStrikesImpl = new NodeOperatorStrikes({ module: address(curatedModule) });

            _upgradeAndHandoffProxy(
                address(nodeOperatorStrikes),
                address(nodeOperatorStrikesImpl),
                abi.encodeCall(NodeOperatorStrikes.initialize, (deployer, config.nodeOperatorStrikesConfig.thresholds))
            );

            // LDO lock boost provider
            {
                ERC20LockBoostProviderConfig storage ldoConfig = config.ldoLockBoostProviderConfig;

                ldoLockVaultImpl = new LidoGovernanceLockVault({
                    token: ldoConfig.token,
                    provider: address(ldoLockBoostProvider),
                    module: address(curatedModule),
                    votingContract: ldoConfig.votingContract,
                    snapshotDelegation_: ldoConfig.snapshotDelegation
                });
                ldoLockVaultBeacon = new UpgradeableBeacon(address(ldoLockVaultImpl), deployer);

                ldoLockBoostProviderImpl = new ERC20LockBoostProvider({
                    module: address(curatedModule),
                    token: ldoConfig.token,
                    vaultBeacon: address(ldoLockVaultBeacon),
                    minLockPeriod: ldoConfig.minLockPeriod
                });

                _upgradeAndHandoffProxy(
                    address(ldoLockBoostProvider),
                    address(ldoLockBoostProviderImpl),
                    abi.encodeCall(
                        ERC20LockBoostProvider.initialize,
                        (deployer, ldoConfig.lockPeriod, ldoConfig.lockBoostSteps)
                    )
                );
            }

            customFeeRegistryImpl = new CustomFeeRegistry({ module: address(curatedModule) });

            _upgradeAndHandoffProxy(
                address(customFeeRegistry),
                address(customFeeRegistryImpl),
                abi.encodeCall(
                    CustomFeeRegistry.initialize,
                    (
                        deployer,
                        config.customFeeRegistryConfig.feeShareDiscountCutCooldown,
                        config.customFeeRegistryConfig.boostSteps
                    )
                )
            );

            accounting.grantRole(accounting.MANAGE_BOND_CURVES_ROLE(), address(deployer));
            accounting.grantRole(accounting.SET_BOND_CURVE_MULTIPLIER_ROLE(), address(additionalBondRegistry));
            _addWeightBoostProvider(
                address(additionalBondRegistry),
                IMetaRegistry.WeightBoostProviderMode.PerNodeOperator
            );
            _addWeightBoostProvider(
                address(nodeOperatorStrikes),
                IMetaRegistry.WeightBoostProviderMode.PerNodeOperator
            );
            _addWeightBoostProvider(address(ldoLockBoostProvider), IMetaRegistry.WeightBoostProviderMode.MaxPerGroup);
            _addWeightBoostProvider(address(customFeeRegistry), IMetaRegistry.WeightBoostProviderMode.PerNodeOperator);
            nodeOperatorStrikes.grantRole(
                nodeOperatorStrikes.STRIKES_COMMITTEE_ROLE(),
                config.nodeOperatorStrikesConfig.committee
            );
            metaRegistry.grantRole(metaRegistry.SET_BOND_CURVE_WEIGHT_ROLE(), deployer);

            for (uint256 i = 0; i < gatesCount; i++) {
                CuratedGateConfig storage gateConfig = config.curatedGates[i];
                // default curve if no values
                uint256 curveId = 0;
                if (gateConfig.bondCurve.length != 0) {
                    IBondCurve.BondCurveIntervalInput[] memory curatedGateBondCurve = CommonScriptUtils
                        .arraysToBondCurveIntervalsInputs(gateConfig.bondCurve);
                    curveId = accounting.addBondCurve(curatedGateBondCurve);
                }
                curatedCurveIds[i] = curveId;

                GateCurveParams storage params = gateConfig.params;
                if (params.generalDelayedPenaltyAdditionalFine.isValue) {
                    parametersRegistry.setGeneralDelayedPenaltyAdditionalFine(
                        curveId,
                        params.generalDelayedPenaltyAdditionalFine.value
                    );
                }
                if (params.keysLimit.isValue) {
                    parametersRegistry.setKeysLimit(curveId, params.keysLimit.value);
                }
                if (params.avgPerfLeewayData.length > 0) {
                    parametersRegistry.setPerformanceLeewayData(
                        curveId,
                        CommonScriptUtils.arraysToKeyIndexValueIntervals(params.avgPerfLeewayData)
                    );
                }
                if (params.rewardShareData.length > 0) {
                    parametersRegistry.setRewardShareData(
                        curveId,
                        CommonScriptUtils.arraysToKeyIndexValueIntervals(params.rewardShareData)
                    );
                }
                if (params.strikesLifetimeFrames.isValue || params.strikesThreshold.isValue) {
                    parametersRegistry.setStrikesParams(
                        curveId,
                        params.strikesLifetimeFrames.value,
                        params.strikesThreshold.value
                    );
                }
                if (params.badPerformancePenalty.isValue) {
                    parametersRegistry.setBadPerformancePenalty(curveId, params.badPerformancePenalty.value);
                }
                if (params.attestationsWeight.isValue || params.blocksWeight.isValue || params.syncWeight.isValue) {
                    parametersRegistry.setPerformanceCoefficients(
                        curveId,
                        params.attestationsWeight.value,
                        params.blocksWeight.value,
                        params.syncWeight.value
                    );
                }
                if (params.metaRegistryBondCurveWeight.isValue) {
                    metaRegistry.setBondCurveWeight(curveId, params.metaRegistryBondCurveWeight.value);
                }
                if (params.allowedExitDelay.isValue) {
                    parametersRegistry.setAllowedExitDelay(curveId, params.allowedExitDelay.value);
                }
                if (params.exitDelayFee.isValue) {
                    parametersRegistry.setExitDelayFee(curveId, params.exitDelayFee.value);
                }
                if (params.maxElWithdrawalRequestFee.isValue) {
                    parametersRegistry.setMaxElWithdrawalRequestFee(curveId, params.maxElWithdrawalRequestFee.value);
                }
            }

            accounting.revokeRole(accounting.MANAGE_BOND_CURVES_ROLE(), address(deployer));
            metaRegistry.revokeRole(metaRegistry.SET_BOND_CURVE_WEIGHT_ROLE(), deployer);

            ValidatorStrikes strikesImpl = new ValidatorStrikes({
                module: address(curatedModule),
                oracle: address(oracle)
            });

            strikes = ValidatorStrikes(_deployProxy(deployer, address(dummyImpl)));

            ExitPenalties exitPenaltiesImpl = new ExitPenalties(address(curatedModule), address(strikes));

            {
                OssifiableProxy exitPenaltiesProxy = OssifiableProxy(payable(address(exitPenalties)));
                exitPenaltiesProxy.proxy__upgradeTo(address(exitPenaltiesImpl));
                exitPenaltiesProxy.proxy__changeAdmin(config.proxyAdmin);
            }

            ejector = new Ejector(address(curatedModule), address(strikes), deployer);

            {
                OssifiableProxy strikesProxy = OssifiableProxy(payable(address(strikes)));
                strikesProxy.proxy__upgradeToAndCall(
                    address(strikesImpl),
                    abi.encodeCall(ValidatorStrikes.initialize, (deployer, address(ejector)))
                );
                strikesProxy.proxy__changeAdmin(config.proxyAdmin);
            }

            curatedGateImpl = address(new CuratedGate(address(curatedModule)));

            curatedGateFactory = new MerkleGateFactory(curatedGateImpl);

            curatedGateInstances = _deployCuratedGates(curatedCurveIds, address(curatedGateFactory));

            hashConsensus = new HashConsensus({
                slotsPerEpoch: config.slotsPerEpoch,
                secondsPerSlot: config.secondsPerSlot,
                genesisTime: config.clGenesisTime,
                epochsPerFrame: config.oracleReportEpochsPerFrame,
                fastLaneLengthSlots: config.fastLaneLengthSlots,
                admin: address(deployer),
                reportProcessor: address(oracle)
            });
            hashConsensus.grantRole(hashConsensus.MANAGE_MEMBERS_AND_QUORUM_ROLE(), config.aragonAgent);
            hashConsensus.grantRole(hashConsensus.MANAGE_MEMBERS_AND_QUORUM_ROLE(), address(deployer));
            for (uint256 i = 0; i < config.oracleMembers.length; i++) {
                hashConsensus.addMember(config.oracleMembers[i], config.hashConsensusQuorum);
            }
            hashConsensus.revokeRole(hashConsensus.MANAGE_MEMBERS_AND_QUORUM_ROLE(), address(deployer));

            FeeOracle oracleImpl = new FeeOracle({
                feeDistributor: address(feeDistributor),
                strikes: address(strikes),
                secondsPerSlot: config.secondsPerSlot,
                genesisTime: config.clGenesisTime
            });

            {
                OssifiableProxy oracleProxy = OssifiableProxy(payable(address(oracle)));
                oracleProxy.proxy__upgradeToAndCall(
                    address(oracleImpl),
                    abi.encodeCall(FeeOracle.initialize, (deployer, address(hashConsensus), config.consensusVersion))
                );
                oracleProxy.proxy__changeAdmin(config.proxyAdmin);
            }

            curatedModule.grantRole(curatedModule.PAUSE_ROLE(), circuitBreaker);
            accounting.grantRole(accounting.PAUSE_ROLE(), circuitBreaker);
            oracle.grantRole(oracle.PAUSE_ROLE(), circuitBreaker);
            verifier.grantRole(verifier.PAUSE_ROLE(), circuitBreaker);
            ejector.grantRole(ejector.PAUSE_ROLE(), circuitBreaker);

            curatedModule.grantRole(curatedModule.PAUSE_ROLE(), config.resealManager);
            curatedModule.grantRole(curatedModule.RESUME_ROLE(), config.resealManager);
            accounting.grantRole(accounting.PAUSE_ROLE(), config.resealManager);
            accounting.grantRole(accounting.RESUME_ROLE(), config.resealManager);
            oracle.grantRole(oracle.PAUSE_ROLE(), config.resealManager);
            oracle.grantRole(oracle.RESUME_ROLE(), config.resealManager);
            verifier.grantRole(verifier.PAUSE_ROLE(), config.resealManager);
            verifier.grantRole(verifier.RESUME_ROLE(), config.resealManager);
            ejector.grantRole(ejector.PAUSE_ROLE(), config.resealManager);
            ejector.grantRole(ejector.RESUME_ROLE(), config.resealManager);

            metaRegistry.grantRole(metaRegistry.SET_OPERATOR_INFO_ROLE(), config.setOperatorInfoManager);
            metaRegistry.grantRole(metaRegistry.MANAGE_OPERATOR_GROUPS_ROLE(), config.easyTrackEVMScriptExecutor);

            parametersRegistry.grantRole(
                parametersRegistry.MANAGE_GENERAL_PENALTIES_AND_CHARGES_ROLE(),
                config.penaltiesManager
            );

            curatedModule.grantRole(
                curatedModule.REPORT_GENERAL_DELAYED_PENALTY_ROLE(),
                config.generalDelayedPenaltyReporter
            );
            curatedModule.grantRole(
                curatedModule.SETTLE_GENERAL_DELAYED_PENALTY_ROLE(),
                config.easyTrackEVMScriptExecutor
            );

            curatedModule.grantRole(curatedModule.VERIFIER_ROLE(), address(verifier));
            curatedModule.grantRole(curatedModule.REPORT_REGULAR_WITHDRAWN_VALIDATORS_ROLE(), address(verifier));
            curatedModule.grantRole(
                curatedModule.REPORT_SLASHED_WITHDRAWN_VALIDATORS_ROLE(),
                config.easyTrackEVMScriptExecutor
            );

            if (config.secondAdminAddress != address(0)) {
                if (config.secondAdminAddress == deployer) revert InvalidSecondAdmin();
                _grantSecondAdmins();
            }

            curatedModule.grantRole(curatedModule.DEFAULT_ADMIN_ROLE(), config.aragonAgent);
            curatedModule.revokeRole(curatedModule.DEFAULT_ADMIN_ROLE(), deployer);

            ejector.grantRole(ejector.DEFAULT_ADMIN_ROLE(), config.aragonAgent);
            ejector.revokeRole(ejector.DEFAULT_ADMIN_ROLE(), deployer);

            parametersRegistry.grantRole(parametersRegistry.DEFAULT_ADMIN_ROLE(), config.aragonAgent);
            parametersRegistry.revokeRole(parametersRegistry.DEFAULT_ADMIN_ROLE(), deployer);

            for (uint256 i = 0; i < curatedGateInstances.length; i++) {
                CuratedGate gate = CuratedGate(curatedGateInstances[i]);
                gate.grantRole(gate.DEFAULT_ADMIN_ROLE(), config.aragonAgent);
                gate.revokeRole(gate.DEFAULT_ADMIN_ROLE(), deployer);
            }

            metaRegistry.grantRole(metaRegistry.DEFAULT_ADMIN_ROLE(), config.aragonAgent);
            metaRegistry.revokeRole(metaRegistry.DEFAULT_ADMIN_ROLE(), deployer);

            additionalBondRegistry.grantRole(additionalBondRegistry.DEFAULT_ADMIN_ROLE(), config.aragonAgent);
            additionalBondRegistry.revokeRole(additionalBondRegistry.DEFAULT_ADMIN_ROLE(), deployer);

            nodeOperatorStrikes.grantRole(nodeOperatorStrikes.DEFAULT_ADMIN_ROLE(), config.aragonAgent);
            nodeOperatorStrikes.revokeRole(nodeOperatorStrikes.DEFAULT_ADMIN_ROLE(), deployer);

            ldoLockBoostProvider.grantRole(ldoLockBoostProvider.DEFAULT_ADMIN_ROLE(), config.aragonAgent);
            ldoLockBoostProvider.revokeRole(ldoLockBoostProvider.DEFAULT_ADMIN_ROLE(), deployer);

            ldoLockVaultBeacon.transferOwnership(config.aragonAgent);
            customFeeRegistry.grantRole(customFeeRegistry.DEFAULT_ADMIN_ROLE(), config.aragonAgent);
            customFeeRegistry.revokeRole(customFeeRegistry.DEFAULT_ADMIN_ROLE(), deployer);

            verifier.grantRole(verifier.DEFAULT_ADMIN_ROLE(), config.aragonAgent);
            verifier.revokeRole(verifier.DEFAULT_ADMIN_ROLE(), deployer);

            accounting.grantRole(accounting.DEFAULT_ADMIN_ROLE(), config.aragonAgent);
            accounting.revokeRole(accounting.DEFAULT_ADMIN_ROLE(), deployer);

            hashConsensus.grantRole(hashConsensus.DEFAULT_ADMIN_ROLE(), config.aragonAgent);
            hashConsensus.revokeRole(hashConsensus.DEFAULT_ADMIN_ROLE(), deployer);

            oracle.grantRole(oracle.DEFAULT_ADMIN_ROLE(), config.aragonAgent);
            oracle.revokeRole(oracle.DEFAULT_ADMIN_ROLE(), deployer);

            feeDistributor.grantRole(feeDistributor.DEFAULT_ADMIN_ROLE(), config.aragonAgent);
            feeDistributor.revokeRole(feeDistributor.DEFAULT_ADMIN_ROLE(), deployer);

            strikes.grantRole(strikes.DEFAULT_ADMIN_ROLE(), config.aragonAgent);
            strikes.revokeRole(strikes.DEFAULT_ADMIN_ROLE(), deployer);

            JsonObj memory deployJson = Json.newObj("artifact");
            deployJson.set("ChainId", chainId);
            deployJson.set("CuratedModule", address(curatedModule));
            deployJson.set("CuratedModuleImpl", address(curatedModuleImpl));
            deployJson.set("MetaRegistry", address(metaRegistry));
            deployJson.set("MetaRegistryImpl", address(metaRegistryImpl));
            deployJson.set("AdditionalBondRegistry", address(additionalBondRegistry));
            deployJson.set("AdditionalBondRegistryImpl", address(additionalBondRegistryImpl));
            deployJson.set("NodeOperatorStrikes", address(nodeOperatorStrikes));
            deployJson.set("NodeOperatorStrikesImpl", address(nodeOperatorStrikesImpl));
            deployJson.set("LDOLockBoostProvider", address(ldoLockBoostProvider));
            deployJson.set("LDOLockBoostProviderImpl", address(ldoLockBoostProviderImpl));
            deployJson.set("LDOLockVaultImpl", address(ldoLockVaultImpl));
            deployJson.set("LDOLockVaultBeacon", address(ldoLockVaultBeacon));
            deployJson.set("CustomFeeRegistry", address(customFeeRegistry));
            deployJson.set("CustomFeeRegistryImpl", address(customFeeRegistryImpl));
            deployJson.set("ParametersRegistry", address(parametersRegistry));
            deployJson.set("ParametersRegistryImpl", address(parametersRegistryImpl));
            deployJson.set("Accounting", address(accounting));
            deployJson.set("AccountingImpl", address(accountingImpl));
            deployJson.set("FeeOracle", address(oracle));
            deployJson.set("FeeOracleImpl", address(oracleImpl));
            deployJson.set("FeeDistributor", address(feeDistributor));
            deployJson.set("FeeDistributorImpl", address(feeDistributorImpl));
            deployJson.set("ExitPenalties", address(exitPenalties));
            deployJson.set("ExitPenaltiesImpl", address(exitPenaltiesImpl));
            deployJson.set("Ejector", address(ejector));
            deployJson.set("ValidatorStrikes", address(strikes));
            deployJson.set("ValidatorStrikesImpl", address(strikesImpl));
            deployJson.set("HashConsensus", address(hashConsensus));
            deployJson.set("Verifier", address(verifier));
            deployJson.set("CuratedGateFactory", address(curatedGateFactory));
            deployJson.set("CuratedGateImpl", curatedGateImpl);
            deployJson.set("CuratedGates", curatedGateInstances);
            deployJson.set("LidoLocator", config.lidoLocatorAddress);
            deployJson.set("CircuitBreaker", circuitBreaker);
            deployJson.set("CuratedDeployParams", abi.encode(config));
            deployJson.set("git-ref", gitRef);
            if (!vm.exists(artifactDir)) {
                vm.createDir(artifactDir, true);
            }
            vm.writeJson(deployJson.str, _deployJsonFilename());
        }

        vm.stopBroadcast();
    }

    function _deployCuratedGates(
        uint256[] memory curveIds,
        address gateFactoryAddress
    ) internal returns (address[] memory gates) {
        uint256 gateCount = curveIds.length;
        if (gateCount == 0) return gates;
        gates = new address[](gateCount);

        if (gateFactoryAddress == address(0)) revert InvalidInput("curated gate factory address is zero");
        MerkleGateFactory gateFactory = MerkleGateFactory(gateFactoryAddress);

        for (uint256 i = 0; i < gateCount; i++) {
            uint256 gateCurveId = curveIds[i];
            CuratedGateConfig storage gateConfig = config.curatedGates[i];
            CuratedGate gate = CuratedGate(
                gateFactory.create(gateCurveId, gateConfig.treeRoot, gateConfig.treeCid, gateConfig.name, deployer)
            );

            {
                OssifiableProxy gateProxy = OssifiableProxy(payable(address(gate)));
                gateProxy.proxy__changeAdmin(config.proxyAdmin);
            }

            gates[i] = address(gate);

            curatedModule.grantRole(curatedModule.CREATE_NODE_OPERATOR_ROLE(), address(gate));
            if (gateCurveId != accounting.DEFAULT_BOND_CURVE_ID()) {
                accounting.grantRole(accounting.SET_BOND_CURVE_ROLE(), address(gate));
            }
            metaRegistry.grantRole(metaRegistry.SET_OPERATOR_INFO_ROLE(), address(gate));
            gate.grantRole(gate.PAUSE_ROLE(), config.curatedGatePauseManager);
            gate.grantRole(gate.SET_TREE_ROLE(), config.easyTrackEVMScriptExecutor);
        }
        return gates;
    }

    /// @dev Points the proxy at `impl`, runs the initializer, and hands proxy administration to the DAO.
    function _upgradeAndHandoffProxy(address proxyAddress, address impl, bytes memory initCalldata) internal {
        OssifiableProxy proxy = OssifiableProxy(payable(proxyAddress));
        proxy.proxy__upgradeToAndCall(impl, initCalldata);
        proxy.proxy__changeAdmin(config.proxyAdmin);
    }

    /// @dev Registration order assigns the provider ids, so keep the call sequence stable.
    function _addWeightBoostProvider(address provider, IMetaRegistry.WeightBoostProviderMode mode) internal {
        metaRegistry.addWeightBoostProvider(IWeightBoostProvider(provider), mode);
    }

    function _deployProxy(address admin, address implementation) internal returns (address) {
        return _deployProxy(admin, implementation, new bytes(0));
    }

    function _deployProxy(address admin, address implementation, bytes memory initCalldata) internal returns (address) {
        OssifiableProxy proxy = new OssifiableProxy({
            implementation_: implementation,
            data_: initCalldata,
            admin_: admin
        });

        return address(proxy);
    }

    function _deployJsonFilename() internal view returns (string memory) {
        return string(abi.encodePacked(artifactDir, "deploy-", chainName, ".json"));
    }

    function _grantSecondAdmins() internal {
        if (keccak256(abi.encodePacked(chainName)) == keccak256("mainnet")) revert CannotBeUsedInMainnet();
        curatedModule.grantRole(curatedModule.DEFAULT_ADMIN_ROLE(), config.secondAdminAddress);
        accounting.grantRole(accounting.DEFAULT_ADMIN_ROLE(), config.secondAdminAddress);
        oracle.grantRole(oracle.DEFAULT_ADMIN_ROLE(), config.secondAdminAddress);
        feeDistributor.grantRole(feeDistributor.DEFAULT_ADMIN_ROLE(), config.secondAdminAddress);
        hashConsensus.grantRole(hashConsensus.DEFAULT_ADMIN_ROLE(), config.secondAdminAddress);
        parametersRegistry.grantRole(parametersRegistry.DEFAULT_ADMIN_ROLE(), config.secondAdminAddress);
        metaRegistry.grantRole(metaRegistry.DEFAULT_ADMIN_ROLE(), config.secondAdminAddress);
        additionalBondRegistry.grantRole(additionalBondRegistry.DEFAULT_ADMIN_ROLE(), config.secondAdminAddress);
        nodeOperatorStrikes.grantRole(nodeOperatorStrikes.DEFAULT_ADMIN_ROLE(), config.secondAdminAddress);
        ldoLockBoostProvider.grantRole(ldoLockBoostProvider.DEFAULT_ADMIN_ROLE(), config.secondAdminAddress);
        customFeeRegistry.grantRole(customFeeRegistry.DEFAULT_ADMIN_ROLE(), config.secondAdminAddress);
        for (uint256 i = 0; i < curatedGateInstances.length; i++) {
            CuratedGate gate = CuratedGate(curatedGateInstances[i]);
            gate.grantRole(gate.DEFAULT_ADMIN_ROLE(), config.secondAdminAddress);
        }
        ejector.grantRole(ejector.DEFAULT_ADMIN_ROLE(), config.secondAdminAddress);
        verifier.grantRole(verifier.DEFAULT_ADMIN_ROLE(), config.secondAdminAddress);
        strikes.grantRole(strikes.DEFAULT_ADMIN_ROLE(), config.secondAdminAddress);
    }
}
